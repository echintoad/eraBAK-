;===========================================================================
;	자원 표시 관련으로 사용하는 함수군
;
;PRINT_TARGET_IMAGE				타겟의 화상을 인원수분 표시　AA의 장소에서 부른다
;PRINT_STATE_IMAGE				상태화면의 표시 전용
;PRINT_IMAGE					어디라도 부르면 그림표시 해 주는 함수
;PRINT_IMAGE_ANIMATION			연번 화상 애니메이션
;PRINT_IMAGE_ANIMATION_정상위	정상위의 애니메이션 그림표시
;PRINT_IMAGE_ANIMATION_후배위	후배위의 애니메이션 그림표시
;PRINT_IMAGE_ANIMATION_사정후	사정후의 애니메이션 그림표시
;
;===========================================================================

;=========================================================
;   복장 변경/기타 조건에 따른 화상변화 적용 - 레몬녹차
; 
;   리소스 폴더 내 CSV에 화상 지정 필요
;   CSV에 쓰이는 의상/악세서리 이름은 CLOTH/BODYSUIT & CLOTH/ACCESSORY 참고
;=========================================================
@PRINT_IMAGE_OTHER_CLOTHES(대상)
#DIM 대상
#DIM 옷대상
#DIMS clothes
#DIMS chk

;대상이 플레이어면 복장 참고 대상을 0번 캐릭터로
IF 대상 == FLAG:주인공화상
	옷대상 = 0
ELSE
	옷대상 = 대상
ENDIF

;-------------------------------------------------
; Failsafe (한계선?)
; 얼굴, 통상 표정으로 체크

CALL GET_CLOTH_NAME_CSV(0, CLOTH:옷대상:의상부위_상하의)
clothes = %RESULTS%

; 의상 체크: 최적화 문제시 삭제
; 이벤트/구상 등 의상용 메뉴를 통과하지 않고 의상이 바뀔 상황에 대비용
chk = 얼굴_%clothes%_통상_{옷대상}
IF !SPRITECREATED(chk)
	CALL GET_CLOTH_NAME_CSV(0, 0)
	clothes = %RESULTS%
ENDIF
; 의상 체크 끝

CALL GET_CLOTH_NAME_CSV(1, CLOTH:옷대상:의상부위_악세서리)
chk = 얼굴_%RESULTS%%clothes%_통상_{옷대상}
SIF SPRITECREATED(chk)
	clothes = %RESULTS%%clothes%
; DEBUGPRINTFORML %clothes%
;-------------------------------------------------

RESULTS = _%clothes%
RETURN

@PRINT_IMAGE_CHILD
#DIMS 화상

RESULTS = %화상%

;======================================================
;  ☆ 이미지 출력 함수
;  따로따로 퍼져있는 화상출력을 한 함수로 통합함(by. 레몬녹차)
;  대상 : 캐릭터번호
;  종류 : 스탠딩 혹은 얼굴
;  표정 : 통상, 웃음, 화남
;  출력예시 : 얼굴_화남_통상_13
;======================================================
@PRINT_IMAGE_CHARA(대상, 종류, 표정)
#DIM 대상
#DIMS 표정
#DIMS 종류
#DIMS sName

IF 대상 == -1
	sName = 더미캐릭터
ELSE
	;대상의 주인공 여부 확인
	IF 대상 == 0
		;커스텀 페이스 적용 유무
		IF TALENT:대상:커스텀페이스
			;커스텀페이스(남캐)
			IF IS_MALE(대상)
				sName = 커스텀_M_얼굴_의복_통상_{TALENT:대상:커스텀페이스}
			;커스텀페이스(여캐)
			ELSE
				sName = 커스텀_F_얼굴_의복_통상_{TALENT:대상:커스텀페이스}
			ENDIF
		;주인공이 고정캐릭일 때
		ELSEIF !FLAG:주인공화상 == 0
				CALL PRINT_IMAGE_OTHER_CLOTHES(FLAG:주인공화상)
				sName = %종류%%RESULTS%_%표정%_{FLAG:주인공화상}
		ELSE
			;기본 남캐 화상
			IF IS_MALE(대상)
				sName = 스탠딩_의복_통상_남
			;기본 여캐 화상
			ELSE
				sName = 스탠딩_의복_통상_여
			ENDIF
		ENDIF
	;해당 캐릭터가 고정 캐릭터라면
	ELSEIF IS_FIXED_CHARA(대상)
		CALL PRINT_IMAGE_OTHER_CLOTHES(대상)
		sName = %종류%%RESULTS%_%표정%_{대상}
	;해당 캐릭터가 아이라면
	ELSEIF IS_CHILD(대상)
		TRYCALLFORM ADDON_AI_CHILD_IMAGE_CORE
		IF RESULT == 1
			IF TALENT:대상:아이화상 >= 0
				sName = 아이_AI_%종류%_통상_{TALENT:대상:아이화상}_{TALENT:대상:아이화상번호}
			ELSE
				sName = 아이_AI_%종류%_통상_모브_{TALENT:대상:아이화상번호}
			ENDIF
		ELSE
			sNAME = 더미캐릭터
		ENDIF
	;해당 캐릭터가 특수세력이라면
	ELSEIF IS_SP_CHARA(대상)
		IF CFLAG:대상:소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_카이저_코퍼레이션)
			sName = 스탠딩_의복_통상_PMC
		ELSEIF CFLAG:대상:소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_홉고블린)
			sName = 스탠딩_의복_통상_홉고블린
		ELSEIF CFLAG:대상:소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_외래인)
			sName = 스탠딩_의복_통상_외래인
		ELSEIF CFLAG:대상:소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_촉수)
			sName = 스탠딩_의복_통상_촉수
		ELSEIF CFLAG:대상:소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_자경단)
			sName = 스탠딩_의복_통상_자경단
		ELSEIF CFLAG:대상:소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_서큐버스)
			sName = 스탠딩_의복_통상_서큐버스
		ELSEIF CFLAG:대상:소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_광신도)
			sName = 스탠딩_의복_통상_광신도
		ELSE
			sName = 더미캐릭터
		ENDIF
	ELSEIF IS_RANDOM_CHARA(대상)
		IF TALENT:대상:커스텀페이스
			IF IS_FEMALE(대상)
				sName = 커스텀_F_얼굴_의복_통상_{TALENT:대상:커스텀페이스}
			ELSE
				sName = 커스텀_M_얼굴_의복_통상_{TALENT:대상:커스텀페이스}
			ENDIF
		ELSE
			sName = 더미캐릭터
		ENDIF
	ELSEIF TALENT:대상:커스텀페이스
		;커스텀페이스(남캐)
		IF IS_MALE(대상)
			sName = 커스텀_M_얼굴_의복_통상_{TALENT:대상:커스텀페이스}
		;커스텀페이스(여캐)
		ELSE
			sName = 커스텀_F_얼굴_의복_통상_{TALENT:대상:커스텀페이스}
		ENDIF
	ELSE
		sName = 더미캐릭터
	ENDIF
ENDIF

RESULTS = %sName%
RETURN



;=========================================================
;	갈아입기의 그림표시
;
;	iID		：캐릭터 ID와 같은 자원명을 표시합니다
;	iSize	：폰트에 대한 사이즈%수치세로 기준 종횡비유지 생략으로 1000(10배)
;=========================================================
@PRINT_STATE_IMAGE(iID, eText="통상", iSize=1000)
#DIM iID
#DIM iSize
#DIMS sPrint_Text
#DIMS sTemp, 3
#DIMS eText
#DIM iFont_Hei_mag
#DIM iEnter_Con

;SIF !iID
;	RETURN RESULT

iFont_Hei_mag = GETCONFIG("一行の高さ") * iSize*10 / GETCONFIG("フォントサイズ")
iFont_Hei_mag = (iFont_Hei_mag/10) + (iFont_Hei_mag%10 >= 5? 1#0)

sPrint_Text = <p align='left'>
CALL PRINT_IMAGE_CHARA(iID, "스탠딩", "통상")
sPrint_Text += @"<img src='%RESULTS%' height='{iFont_Hei_mag}'>"
HTML_PRINT sPrint_Text
iEnter_Con = 1

IF FLAG:화상범위표시
	sPrint_Text =  <p align='left'><nobr>
	sPrint_Text += @"<img src='프레임_%eText%' height='{iFont_Hei_mag}'"
	sPrint_Text += @"ypos='-{(iEnter_Con) * iFont_Hei_mag / 10+1}'>"
	HTML_PRINT sPrint_Text
	iEnter_Con++
ENDIF

FOR LOCAL, iEnter_Con+1, (iSize/100)
	HTML_PRINT "<br>"
NEXT
HTML_PRINT "<br>"
HTML_PRINT "<br>"
RETURN RESULT

;=========================================================
;	조교 화면의 그림표시
;
;	iID		：캐릭터 ID와 같은 자원명을 표시합니다
;	iSize	：폰트에 대한 사이즈%수치세로 기준 종횡비유지 생략으로 1000(10배)
;	iEnter_Wid	: 그림의 X축 위치 배율
;=========================================================
@PRINT_PLAY_IMAGE(iSize=1000)
#DIM iID
#DIM iSize
#DIMS sTemp, 3
#DIMS sPrint_Text
#DIMS eMotion_Text
#DIM iFont_Hei_mag
#DIM iEnter_Con
#DIM iEnter_Wid
#DIM dIsplay_Num

dIsplay_Num = 0
iEnter_Con = 0
iEnter_Wid = 0
iFont_Hei_mag = GETCONFIG("一行の高さ") * iSize*10 / GETCONFIG("フォントサイズ")
iFont_Hei_mag = (iFont_Hei_mag/10) + (iFont_Hei_mag%10 >= 5? 1#0)

VARSET sTemp


;뒷부분 최대 캐릭터 수
FOR iID, 0, CHARANUM
;	IF CFLAG:(iID):조교참가플래그
	IF IS_PARTICIPATE_TRAIN(iID)
		;-----------------
		;복장 변경에 따른 화상변경 (조교시)
		;-----------------

		SIF FLAG:그림표시 == dIsplay_Num
			BREAK
		IF dIsplay_Num == 5
			iEnter_Con = 0
			iEnter_Wid = 0
		ENDIF
		eMotion_Text = %TOSTR_EMOTION(iID)%
		IF GROUPMATCH(eMotion_Text, "행복", "기쁨")
			sTemp:1 = 미소
		ELSEIF GROUPMATCH(eMotion_Text, "원한", "분노", "분개")
			sTemp:1 = 분노
		ELSEIF GROUPMATCH(eMotion_Text, "암울", "슬픔", "우울")
			sTemp:1 = 슬픔
		ELSEIF GROUPMATCH(eMotion_Text, "공포", "겁냄")
			sTemp:1 = 공포
		ELSEIF GROUPMATCH(eMotion_Text, "광기")
			sTemp:1 = 광기
		ELSEIF GROUPMATCH(eMotion_Text, "최면", "공허", "붕괴")
			sTemp:1 = 공허
		ELSE
			sTemp:1 = 통상
		ENDIF

		IF FLAG:스탠딩종류 == 0
			sTemp:2 = 스탠딩
		ELSEIF FLAG:스탠딩종류 == 1
			sTemp:2 = 얼굴
		ENDIF
		sPrint_Text = <p align='left'><shape type = 'space'param = '{1000 * (iEnter_Wid)}'>
		;화상표시 함수
		CALL PRINT_IMAGE_CHARA(iID, sTemp:2, sTemp:1)
		sPrint_Text += @"<img src='%RESULTS%' height='{iFont_Hei_mag}' ypos='-{(iEnter_Con) * iFont_Hei_mag / 10+1}'>"

		HTML_PRINT sPrint_Text
		iEnter_Con++
		IF FLAG:화상범위표시
			sPrint_Text =  <p align='left'><nobr><shape type = 'space'param = '{1000 * (iEnter_Wid)}'>
			sPrint_Text += @"<img src='프레임_%sTemp:1%' height='{iFont_Hei_mag}'"
			sPrint_Text += @"ypos='-{(iEnter_Con) * iFont_Hei_mag / 10+1}'>"
			HTML_PRINT sPrint_Text
		ELSE
			sPrint_Text =  <p align='left'><nobr><shape type = 'space'param = '{1000 * (iEnter_Wid)}'>
			sPrint_Text += @"<img src='프레임_공백' height='{iFont_Hei_mag}'"
			sPrint_Text += @"ypos='-{(iEnter_Con) * iFont_Hei_mag / 10+1}'>"
			HTML_PRINT sPrint_Text
		ENDIF
		iEnter_Con++
		iEnter_Wid++
		dIsplay_Num++
	ENDIF
NEXT

IF dIsplay_Num > 5
	FOR LOCAL, dIsplay_Num - 5, 5
		HTML_PRINT "<br>"
		HTML_PRINT "<br>"
	NEXT
ELSE
	FOR LOCAL, dIsplay_Num, 5
		HTML_PRINT "<br>"
		HTML_PRINT "<br>"
	NEXT
ENDIF

RETURN RESULT

;=========================================================
;	자원의 표시
;	표시 후 사이즈에 맞추어 개행을 실시한다
;	PRINT_IMG 의 확장같은 느낌 문중의 삽화적으로 사용할 수 있다고 생각한다
;
;	sRes_Name	：자원의 이름 캐릭터라인
;	iSize		：일행의 높이에 대한 사이즈%수치세로 기준 종횡비유지 생략으로 1000(10배)
;	sAlign		：align 속성 생략으로 왼쪽 맞춤　left, center, right의 삼종을 지정할 수 있습니다
;=========================================================
@PRINT_IMAGE(sRes_Name, iSize=1000, sAlign="left")
#DIMS sRes_Name
#DIM iSize
#DIMS sAlign
#DIMS sPrint_Text
#DIM iEnter_Con
#DIM iFont_Hei_mag

SIF GETCONFIGS("描画インターフェース") == "WINAPI"
	THROW 描画インターフェース를 TextRenderer로 변경해 주세요

SIF !FLAG:그림표시 || sRes_Name == ""
	RETURN RESULT

iEnter_Con = 0
iFont_Hei_mag = GETCONFIG("一行の高さ") * iSize*10 / GETCONFIG("フォントサイズ")
iFont_Hei_mag = (iFont_Hei_mag/10) + (iFont_Hei_mag%10 >= 5? 1#0)

HTML_PRINT @"<p align='%sAlign%'><nobr><img src='%sRes_Name%' height='{iFont_Hei_mag}'>"
iEnter_Con++

IF FLAG:화상범위표시
	sPrint_Text =  <p align='%sAlign%'><nobr>
	sPrint_Text += @"<img src='프레임_통상' height='{iFont_Hei_mag}'"
	sPrint_Text += @"ypos='-{iEnter_Con * iFont_Hei_mag / 10+1}'>"
	HTML_PRINT sPrint_Text
	iEnter_Con++
ENDIF


FOR LOCAL, iEnter_Con-1, (iSize/100)-1
	HTML_PRINT "<br>"
NEXT
RETURN RESULT

;=========================================================
;	연번 화상 애니메이션
;
;	sRes_Name	：자원의 이름 캐릭터라인 이 이름의 뒤에 연번 숫자를 붙인다
;	iStart		：개시 번호
;	iEnd		：개시부터의 묘화 매수
;	iAnime_Num	：전체의 애니메이션 반복 회수 생략 0으로 옵션 설정한 플래그 회수가 됩니다
;	iRollback	：롤백(rollback) 시킬까 플래그
;	iWait		：1매의 표시 시간
;	iSize		：일행의 높이에 대한 사이즈%수치세로 기준 종횡비유지 생략으로 1000(10배)
;	sAlign		：align 속성 생략으로 왼쪽 맞춤　left, center, right의 삼종을 지정할 수 있습니다
;=========================================================
@PRINT_IMAGE_ANIMATION(sRes_Name,iStart=0,iEnd=4,iAnime_Num=0,iRollback=1,iWait=100,iSize=1000,sAlign="left")
#DIMS sRes_Name
#DIM iStart
#DIM iEnd
#DIM iAnime_Num
#DIM iRollback
#DIM iWait
#DIM iSize
#DIMS sAlign


SIF !FLAG:그림표시 || sRes_Name == ""
	RETURN RESULT

;SIF !FLAG:애니메이션
;	RETURN RESULT

SIF !iAnime_Num
	iAnime_Num = FLAG:애니메이션

FOR LOCAL,0,iAnime_Num
	FOR LOCAL:1,iStart,(iStart+iEnd)
		CALL PRINT_IMAGE(@"%sRes_Name%{LOCAL:1}",iSize,sAlign)
		TWAIT iWait, 0
		CLEARLINE iSize/100
	NEXT
	
	;ロールバックさせるか
	SIF !iRollback
		CONTINUE

	FOR LOCAL:1,(iStart+iEnd)-2,iStart-1,-1
		CALL PRINT_IMAGE(@"%sRes_Name%{LOCAL:1}",iSize,sAlign)
		TWAIT iWait, 0
		CLEARLINE iSize/100
	NEXT
NEXT
RETURN RESULT

;=========================================================
;	VS 화면의 그림표시
;
;	iID		：캐릭터 ID와 같은 자원명을 표시합니다
;	iSize	：폰트에 대한 사이즈%수치세로 기준 종횡비유지 생략으로 1000(10배)
;=========================================================
@PRINT_VS(VS_1, VS_2)
#DIM VS_1
#DIM VS_2
#DIMS TEMP_1
#DIMS TEMP_2
#DIMS sPrint_Text,3

IF FLAG:화상설정
	HTML_PRINT "<br>"
	HTML_PRINT "<br>"
	HTML_PRINT "<br>"
	HTML_PRINT "<br>"

	;공격자 표시
	; try call print vs potrait
	; CALL PRINT_IMAGE_CHARA(VS_1, "스탠딩", "통상")
	CALL GET_IMAGE_BATTLE_DEFAULT(VS_1, "스탠딩")
	sPrint_Text:0 = <p align='left'>
	sPrint_Text:0 += @"<shape type='space' param='0'><img src='%RESULTS%' height='1000' ypos='-400'>"
	HTML_PRINT sPrint_Text:0

	;VS 표시
	sPrint_Text:1 = <p align='left'>
	sPrint_Text:1 += @"<shape type='space' param='900'><img src='VS_P' height='1000' ypos='-500'>"
	HTML_PRINT sPrint_Text:1

	;방어자 표시
	; try call print vs potrait
	; CALL PRINT_IMAGE_CHARA(VS_2, "스탠딩", "통상")
	CALL GET_IMAGE_BATTLE_DEFAULT(VS_2, "스탠딩")
	sPrint_Text:2 = <p align='left'>
	sPrint_Text:2 += @"<shape type='space' param='1350'><img src='%RESULTS%' height='1000' ypos='-600'>"
	HTML_PRINT sPrint_Text:2

	HTML_PRINT "<br>"
	HTML_PRINT "<br>"
	HTML_PRINT "<br>"
	HTML_PRINT "<br>"
ELSE

ENDIF
RETURN


@FACE_IMAGE(ARG, iSize, iSpace = 0, iChk = 0)
#DIMS sImageName
#DIMS sFrame
#DIMS sWeapon
#DIM iSize
#DIM iPadding
#DIM iSpace
#DIM iVar
#DIM iChk

sImageName = %GET_IMAGE_NAME(ARG)%
sFrame = Frame_1
sWeapon = 웨폰_{TALENT:ARG:웨폰}

iPadding = iSize - 3

FOR iVar, 0, iPadding
	HTML_PRINT "<br>"
NEXT

CALL PRINT_HTML_IMAGE(sImageName, iSize, iPadding, iSpace)
CALL PRINT_HTML_IMAGE(sFrame, iSize, iPadding + 1, iSpace)
SIF iChk == 0
	CALL PRINT_HTML_IMAGE(sWeapon, iSize, iPadding + 2, iSpace + 4)

RETURN

@PRINT_HTML_IMAGE(sImageName, iHeight, iPos, iSpace)
#DIMS sImageName
#DIM iHeight
#DIM iPos
#DIM iSpace
#DIM iSizeFactor
#DIMS sHtml

iSizeFactor = 100 * GETCONFIG("一行の高さ") / GETCONFIG("フォントサイズ")
iHeight *= iSizeFactor
iPos *= iSizeFactor
iSpace *= iSizeFactor
HTML_PRINT @"<shape type='space' param='{iSpace+10}'><img src='%sImageName%' height='{iHeight}' ypos='-{iPos}'>"

RETURN

@GET_IMAGE_NAME(iID)
#FUNCTIONS
#DIM iID
#DIMS sName

IF iID < 0
	sName = 더미캐릭터
	RETURNF sName
ENDIF

IF TALENT:(iID):커스텀페이스 > 0
	sName = 커스텀
	;성별전환 커스텀페이스
	IF IS_MALE(iID)
		sName = %sName%_M
	ELSEIF IS_FEMALE(iID)
		sName = %sName%_F
	ENDIF
	sName = %sName%_얼굴_의복_통상_{TALENT:(iID):커스텀페이스}
ELSEIF EXIST_PICTURE(NO:iID)
	sNAME = 얼굴_의복_통상_{NO:iID}
ELSEIF HAS_TAG(iID,태그_PMC)
	sNAME = 스탠딩_의복_통상_PMC
ELSEIF IS_MALE(iID)
	sName = F_남
ELSEIF IS_FEMALE(iID)
	sName = F_여
ELSE
	sName = 더미캐릭터
ENDIF

RETURNF sName

;-------------------------------------------------------------
;	전투 화상 이미지 이름
;	VS 화면과 총력전 용
;	teams: 아군 (team:0, team:1...)
;	enemies: 적군 (enemies:0, enemy:1...)
;	종류: 스탠딩 혹은 얼굴 (지도자 캐릭터 스탠딩)
;
;	화상 없는 캐릭터면 GET_IMAGE_NAME
;
;	Battle_IMAGE.ERB 에서 없으면
;	CHARA_Battle.csv 에서 전투_B{무작위}_{NO:iID} (showDefault 이면 B0 으로 고정)
;	없으면 %종류%_의복_통상_{NO:iID}
;-------------------------------------------------------------
@GET_IMAGE_BATTLE(iID, teams, enemies, 종류 = "얼굴", showDefault = 0)
#DIM iID
#DIM REF teams, 0
#DIM REF enemies, 0
#DIMS DYNAMIC 종류
#DIM DYNAMIC showDefault ; 1 이면 무작위 무시

#DIM CONST MAX_BATTLE_IMG = 20 ; 다른 곳도 battle.csv 쓰기 시작 하면 .ERH 로 이동
#DIM image_counter
#DIMS DYNAMIC sName
#DIM DYNAMIC temp ; LOCAL 대신

; 화상 없는 캐릭터면 GET_IMAGE_NAME 로
IF !EXIST_PICTURE(iID)
	; DEBUGPRINTL getting GET_IMAGE_NAME
	RESULTS = %GET_IMAGE_NAME(iID)%
	RETURN
ENDIF

; showDefault 면 무시하고 전투_B0_{NO:iID}, 없으면 GET_IMAGE_NAME 로
IF showDefault
	sName = %종류%_전투_B0_{NO:iID}
	IF SPRITECREATED(sName)
		RESULTS = %sName%
		RETURN
	ELSE
		GOTO IMAGE_BATTLE_FALLBACK
	ENDIF
ENDIF

; DEBUGPRINTFORM teams:
; VARSIZE teams
; FOR LOCAL:0, 0, RESULT
; 	DEBUGPRINTFORM %LOCAL:0 == 0 ? "" # "/"% {teams:(LOCAL:0)}
; NEXT
; DEBUGPRINTL
; BATTLE_IMAGE_{NO:iID} 있는지, 없으면 BATTLE 무작위
TRYCCALLFORM BATTLE_IMAGE_{NO:iID}(teams, enemies, 종류)
	RETURN
CATCH
	sName = %종류%_전투_B0_{NO:iID}
	IF SPRITECREATED(sName)
		FOR temp, 1, MAX_BATTLE_IMG
			sName = %종류%_전투_B{temp}_{NO:iID}
			IF !SPRITECREATED(sName)
				image_counter = temp
				BREAK
			ENDIF
		NEXT
		RESULTS = %종류%_전투_B{RAND:image_counter}_{NO:iID}
		RETURN
	ELSE
		GOTO IMAGE_BATTLE_FALLBACK
	ENDIF
ENDCATCH

$IMAGE_BATTLE_FALLBACK
RESULTS = %종류%_의복_통상_{NO:iID}
RETURN

;편의성 함수
@GET_IMAGE_BATTLE_DEFAULT(iID, 종류 = "얼굴")
#DIM iID
#DIMS DYNAMIC 종류

; #DIMS sName
#DIM empty = -1

CALL GET_IMAGE_BATTLE(iID, empty, empty, 종류, 1)
; DEBUGPRINTFORML AFTER Function %RESULTS%
RETURN

;-------------------------------------------------------------
;화상이 있는 캐릭터
;-------------------------------------------------------------
@EXIST_PICTURE(ID)
#FUNCTION
#DIM ID

SELECTCASE ID
CASE 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18, 19, 20, 21, 22, 23, 24, 25, 26, 27, 28, 29, 30, 31, 32, 33, 34, 35, 36, 37, 38, 39, 40, 41, 42, 43, 44, 45, 46, 47, 48, 49, 50, 51, 52, 53, 54, 55, 56, 57, 58, 59, 60, 61, 62, 63, 64, 65, 66, 67, 68, 69, 70, 71, 72, 73, 74, 75, 76, 77, 78, 79, 80, 81, 82, 83, 84, 85, 86, 87, 88, 89, 90, 91, 92, 93, 94, 95, 96, 97, 98, 99, 100, 101, 102, 103, 104, 105, 106, 107, 108, 109, 110, 111, 112, 113, 114, 115, 116, 117, 118, 119, 120, 121, 122, 123, 124, 125, 126, 127, 128, 129, 130, 131, 132, 133, 134, 135, 136, 137, 138, 139, 140, 141, 142
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

;-------------------------------------------------------------
;PRINT COMMANDER
;전투시 지휘관 얼굴을 출력하는 함수
; change to get commander and other side in array
;-------------------------------------------------------------
@PRINT_COMMANDER(teams, enemies)
#DIM REF teams, 0
#DIM REF enemies, 0

#DIMS IMAGE, MAX_UNIT_COMMANDER
#DIM iSize
#DIM iSpace
#DIM iPadding
#DIM iVar

iSize = 6
CALL PRINT_ENTER(iSize - MAX_UNIT_COMMANDER)
FOR iVar, 0, MAX_UNIT_COMMANDER
	VARSIZE teams
	IF  iVar >= RESULT || teams:iVar < 0
		PRINTL
		CONTINUE
	ENDIF
	; IMAGE:iVar = %GET_IMAGE_NAME(COMMANDER:iVar)%
	CALL GET_IMAGE_BATTLE(teams:iVar, teams, enemies)
	IMAGE:iVar = %RESULTS%

	iSpace = iSize * iVar
	iPadding = iSize - MAX_UNIT_COMMANDER + iVar
	CALL F_PRINT_IMAGE(IMAGE:iVar, iSize, iPadding, iSpace)
NEXT

RETURN
;-------------------------------------------------------------
; count만큼 빈 줄을 추가
;-------------------------------------------------------------
@PRINT_ENTER(iCount)
#DIM iCount
#DIM iVar

FOR iVar, 0, iCount
	HTML_PRINT "<br>"
NEXT
 
;=======================================================================
; ☆ 아이 이미지 적용 - 레몬녹차
; * ERB\R_CHARA\CHILD.ERB의 @CREATE_CHILD와 연결됩니다.
; 
; ☆ 함수 설명
; 아이가 태어나면 화상을 지정해준다.
; ○ 기본설정 : 학교 태그가 있다면 해당 학교의 모브
; ○ AI 추가 이미지(확장)
;   - 고정캐릭터의 아이 -> 고정 캐릭을 닮은 화상
;   - 랜덤캐릭터의 아이(주인공 포함) -> 학원 태그에 맞는 화상(태그가 없다면 랜덤)
;
;=======================================================================
@CHILD_IMAGE_SETTING(TARGET, MOTHER)
#DIM MOTHER

LOCAL:0 = -1
;☆ 엄마가 고정캐릭터인지 확인
IF MOTHER == 0 && FLAG:주인공화상 != 0
	;PRINTFORMW 주인공이 엄마 + 주인공이 고정캐릭터
	LOCAL:0 = FLAG:주인공화상
ELSEIF IS_FIXED_CHARA(MOTHER)
	;PRINTFORMW 엄마가 고정캐릭터
	LOCAL:0 = MOTHER
;엄마가 아이 캐릭터이고 고정캐릭이라면
ELSEIF IS_CHILD(MOTHER) && TALENT:MOTHER:아이화상 >= 0
	LOCAL:0 = TALENT:MOTHER:아이화상
ENDIF


;아이 확인
SELECTCASE LOCAL:0
	CASE 1
		;시로코 아이
		TALENT:TARGET:아이화상 = 1
		TALENT:TARGET:아이화상번호 = 1
	CASE 62
		;아이리 아이
		;PRINTFORML 아이리 설정완료
		TALENT:TARGET:아이화상 = 62
		TALENT:TARGET:아이화상번호 = 1
	CASEELSE
		SELECTCASE RAND:1
			CASE 0
				TALENT:TARGET:아이화상 = -1
				TALENT:TARGET:아이화상번호 = 1
		ENDSELECT
ENDSELECT

;-------------------------------------------------------------
; 전투용 이미지 출력
; - sImageName: 이미지 이름
; - iHeight: 이미지 세로크기
;   (단위는 텍스트 한 줄의 높이, 예를 들어 4를 넣으면 텍스트 4줄 만큼의 크기가 됨)
; - iPos: 이미지 세로위치, 단위는 iHeight와 동일
; - iSpace: 이미지 왼쪽 여백, 여러 이미지를 나란히 표시할 때 사용, 기본 0
;-------------------------------------------------------------
@F_PRINT_IMAGE(sImageName, iHeight, iPos, iSpace)
#DIMS sImageName
#DIM iHeight
#DIM iPos
#DIM iSpace
#DIM iSizeFactor
#DIMS sHtml

iSizeFactor = 100 * GETCONFIG("一行の高さ") / GETCONFIG("フォントサイズ")
iHeight *= iSizeFactor
iPos *= iSizeFactor
iSpace *= iSizeFactor
HTML_PRINT @"<shape type='space' param='{iSpace}'><img src='%sImageName%' height='{iHeight}' ypos='-{iPos}'>"

RETURN

;-------------------------------------------------------------
; 무기 이미지 호출이 얼굴과 연동돼있어서 따로 하나 더 작성함
;-------------------------------------------------------------

@WEAPON_IMAGE(ARG, iSize, iSpace = 0)
#DIMS sWeapon
#DIM iSize
#DIM iPadding
#DIM iSpace
#DIM iVar

sWeapon = 웨폰_{ARG}
iPadding = iSize - 3

FOR iVar, 0, iPadding
	HTML_PRINT "<br>"
NEXT
CALL PRINT_HTML_IMAGE(sWeapon, iSize, iPadding, iSpace)

RETURN
