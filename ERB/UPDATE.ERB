;-------------------------------------------------
;준 버젼 문자열에 대응하는 버젼 수치의 취득
;-------------------------------------------------
@GET_VERSION_VALUE(버젼문자열)
#FUNCTION
#DIMS 버젼문자열
;RESULT 퇴피
LOCAL = RESULT
SPLIT 버젼문자열, ".", LOCALS
RESULT = LOCAL

RETURNF TOINT(LOCALS:0) * 10000 + TOINT(LOCALS:1) * 100 + TOINT(LOCALS:2)
;-------------------------------------------------
;준 버젼 수치에 대응하는 버젼 문자열의 취득
;-------------------------------------------------
@GET_VERSION_STRING(버젼수치)
#FUNCTIONS
#DIM 버젼수치

RETURNF TOSTR(버젼수치 / 10000) + "." + TOSTR(버젼수치 % 10000 / 100) + "." + TOSTR(버젼수치 % 10000 % 100)
;-------------------------------------------------
;준 버젼 문자열과 지금의 버젼을 비교해, 아프데가 필요한가 조사한다
;-------------------------------------------------
@CHECK_UPDATE(버젼문자열)
#FUNCTION
#DIMS 버젼문자열

RETURNF LASTLOAD_VERSION < GET_VERSION_VALUE(버젼문자열)

;-------------------------------------------------
;세이브 데이터의 업데이트 처리
;-------------------------------------------------
@UPDATE
#DIM 강제인계플래그
#DIM DYNAMIC cnt

강제인계플래그 = 0

IF LASTLOAD_VERSION != GAMEBASE_VERSION
	PRINTFORML 업데이트 처리를 실시합니다
	PRINTFORML 「엔들레스 루프의 혐의가 운운」이라고 나올 가능성이 있습니다만, 문제는 없기에 속행해 주세요
	PRINTFORMW
	CALL CLEAN_SPERM_MAP()
	; 이전 버전 세이브 CSTR 업데이트용
	; CSV 숫자 +1 만큼 루프 반복
	FOR COUNT:0, 1, 143
		FOR COUNT:1, 0, 100
			IF CSTR:(COUNT:0):(COUNT:1) != ""
;				PRINTFORML {COUNT:0}{COUNT:1} %CSTR:(COUNT:0):(COUNT:1)%
;				PRINTFORML {COUNT:0}{COUNT:1} %CSVCSTR(COUNT:0, COUNT:1)%
				CSTR:(COUNT:0):(COUNT:1) = %CSVCSTR(COUNT:0, COUNT:1)%
;				PRINTFORML {COUNT:0} %CSTR:(COUNT:0):(COUNT:1)%
			ENDIF
		NEXT
;		PRINTFORML %NAME:(COUNT:0)%
;		PRINTFORML %CALLNAME:(COUNT:0)%
		NAME:(COUNT:0) = %CSVNAME(COUNT:0)%
		CALLNAME:(COUNT:0) = %CSVCALLNAME(COUNT:0)%
;		PRINTFORML {COUNT:0} %NAME:(COUNT:0)%
;		PRINTFORML {COUNT:0} %CALLNAME:(COUNT:0)%
	NEXT
	PRINTFORMW 변경된 캐릭터네임을 업데이트했습니다
ENDIF

IF CHECK_UPDATE("0.1.2")
	CALL COLOR_PRINTW("Ver0.1.2", 칼라_주의)
	PRINTFORML + 의복 변경에 따른 화상 변경(치나츠, 히나)
	PRINTFORML + 의복(한벌옷) 종류 및 이름 변경
ENDIF

IF CHECK_UPDATE("0.3.8");랜덤/아이 캐릭터 장비 리셋
	PRINTFORML
	PRINTFORML 0.3.7 버전 이하에 있는 장비 버그를 고치기 위해 랜덤/아이/특수세력 캐릭터 장비를 리셋합니다
	PRINTFORMW 장비를 빼고 싶은 랜덤/아이/특수세력 캐릭터가 있으면 종료 -> 전 버전으로 빼고 돌아오시기를 바랍니다
	FOR COUNT, 0, CHARANUM
		IF IS_RANDOM_CHARA(COUNT) || IS_CHILD(COUNT) || IS_SP_CHARA(COUNT)
			PRINTFORML ID:{NO:COUNT} 장비 리셋
			FOR LOCAL, 0, 3
				착용장비:COUNT:LOCAL = -1
			NEXT
		ENDIF
	NEXT
ENDIF

IF CHECK_UPDATE("0.3.9");의상 리셋
	{
PRINTFORML \n== 0.3.9 버전에 의상 이름, 번호가 바뀌었습니다 ==
\n의상을 초기화 합니까?\n
	}
	PRINTBUTTON "0[예(추천)]", 0
	PRINTL
	PRINTBUTTON "1[아니오]", 1
	INPUT
	SELECTCASE RESULT
		CASE 0
		; 고정 캐릭터만 리셋
			FOR cnt, 1, 143
				CALL CLOTH_INIT(cnt)
			NEXT
			PRINTL 의상 초기화 완료
		CASE 1
			PRINTL 의상 초기화를 건너뜁니다
	ENDSELECT
ENDIF

IF CHECK_UPDATE("0.5.2");전투 화상 무작위 설정
	{
PRINTFORMW \n== 0.5.2 버전에 전투 화상 무작위 설정이 가능하게 되었습니다 ==
\n"전투 무작위화: ON" 으로 합니다
\n캐릭터 그림 설정 에서 변경 가능 합니다
	}
	FLAG:전투화상무작위화 = 1
ENDIF

IF 강제인계플래그
	FLAG:클리어직후플래그 = 1
	CALL SAVE_GAME
	FLAG:클리어직후플래그 = 0
	BEGIN TITLE
ENDIF