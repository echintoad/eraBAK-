;-----------------------------------
;기본치 계산에 앞서 처리하는 것
;-----------------------------------
@SKILL_1201_SP_0_EXIST
RETURN 1

@SKILL_1201_SP_0_NAME
RESULTS = 화끈한 거, 좋아하지?

;레벨은 1-5까지
@SKILL_1201_SP_0_LEVEL
RETURN 5

;대상 선택
@SKILL_1201_SP_0_SETTARGET(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
;부대 대상
RETURN 1

;발동 판정
@SKILL_1201_SP_0_CAN_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측

RETURN 1

;발동 텍스트를 오바라이드 하고 싶을 때에.
;「아무개의 스킬 발동!   00했다!」의 「00했다!」의 부분을 실장하고 싶은 경우는,
;이것이 아니고 INVOKE로 쓰면 좋습니다.
;@SKILL_1201_SP_0_INVOKE_TEXT(발동자, 스킬, 장르)
;#DIM 발동자
;#DIM 스킬
;#DIMS 장르

;효과를 여기에 기술
@SKILL_1201_SP_0_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
; PRINTFORML "가라가라가라가라~!"
LOCAL:1 = RAND(5, 15)
FOR LOCAL, 0, LOCAL:1
	CALL DECREASE_SOLDIER(대상세력, 대상부대, RAND(ABL:발동자:무투*7,ABL:발동자:무투*8), 1)
NEXT


CALL GET_UNIT_COMMANDER_ALL(발동세력, 발동부대)
IF RESULT:0 != 24 && RESULT:1 != 24 && RESULT:2 != 24
	PRINTL 아군 데미지!
	LOCAL:1 = RAND(4, 8)
	FOR LOCAL, 0, LOCAL:1
	CALL DECREASE_SOLDIER(발동세력, 발동부대, RAND(ABL:발동자:무투*5,ABL:발동자:무투*6), 1)
	NEXT
ENDIF


@SKILL_1201_SP_0_EXPLANATION
RESULTS = 적 부대와 아군 부대에 연속 데미지를 준다.


@SKILL_1201_SP_0_CANT_TELL


@SKILL_1201_SP_0_RATE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
RETURN 700

; #region == 총력전 ==
@총력전_스킬_1201_코스트(시전자)
#DIM 시전자 ; 코스트 당 대미지 스킬 카운터 위해서
; #DIM 가변
RETURN 4

; alt.opt.1 버프 지속 4, 공격 후 버프
; 연속 스킬 시전 유도?
@총력전_스킬_1201_설명(시전자)
#DIM 시전자 ; 가변스킬 설명 위해서
RESULTS = 2범위 적에게 공격력 620％ 대미지후 3턴간 공격력 20％ 증가 공격속도 100％ 증가

@총력전_스킬_1201_발동(시전자)
#DIM 시전자

#DIM DYNAMIC 주목표
#DIM DYNAMIC 범위목표, 총력전_캐릭터수
#DIM DYNAMIC 대미지
#DIM CONST 공격력증가량 = 20
#DIM CONST 공격속도증가량 = 100
#DIM DYNAMIC iTmp
#DIMS DYNAMIC sTmp, 3

; 발동 텍스트
sTmp:0 = 「아하하하하하하!!!」
sTmp:1 = 「가라가라가라가라~!」
sTmp:2 = 「어때, 시원~하지?」
CALL SKILL_1201_SP_0_NAME
CALL 총력전_스킬_공통_발동텍스트(시전자, 25, RESULTS, sTmp)
; 목표 처리
CALL 총력전_시퀀스_목표선택(주목표, 총력전_선택_적군)
; 목표 없으면 스킬 해재
IF 주목표 < 0
	CALL 총력전_스킬_공통_스킬취소(시전자)
	RETURN
ENDIF
; 범위로 변경, 적팀 1 이상이 되면 수정 필요
CALL 총력전_목표_범위(범위목표, 주목표, 1, 1)
; 대미지
대미지 = GETVAR_RAIDSTAT_SUM(시전자, "공격력") * 620
대미지 = DIVIDE_ROUND(대미지, 100)
FOR iTmp, 0, 총력전_캐릭터수
	SIF 범위목표:iTmp < 0
		BREAK
	CALL 총력전_처리_피격(대미지, 범위목표:iTmp, 시전자)
NEXT
; 버프
CALL 총력전_상태효과_추가(시전자, 시전자, 상효_종류_공격력백분율, 공격력증가량, 1, 총력전_방식_지속, 총력전_방식_턴, 3, 1201, 0)
CALL 총력전_상태효과_추가(시전자, 시전자, 상효_종류_공격속도, 공격속도증가량, 1, 총력전_방식_지속, 총력전_방식_턴, 3, 1201, 1)
; 텍스트 추가
sTmp = 3턴간 %조사처리(총력전_이름:시전자, "의")% 공격력 ({공격력증가량}％), 공격 속도 ({공격속도증가량}％) 증가
CALL 총력전_UI_텍스트_추가(sTmp, 칼라_노랑)
; UI 업데이트
CALL 총력전_UI_Main(0)
WAITANYKEY
