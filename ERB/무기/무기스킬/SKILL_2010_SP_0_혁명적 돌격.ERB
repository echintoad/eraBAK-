;-----------------------------------
;기본치 계산에 앞서 처리하는 것
;-----------------------------------
@SKILL_2010_SP_0_EXIST
RETURN 1

@SKILL_2010_SP_0_NAME
RESULTS = 혁명적 돌격

;레벨은 1-5까지
@SKILL_2010_SP_0_LEVEL
RETURN 4

;대상 선택
@SKILL_2010_SP_0_SETTARGET(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
;부대 대상
RETURN 1

;발동 판정
@SKILL_2010_SP_0_CAN_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
RETURN 1


;발동 텍스트를 오바라이드 하고 싶을 때에.
;「아무개의 스킬 발동!   00했다!」의 「00했다!」의 부분을 실장하고 싶은 경우는,
;이것이 아니고 INVOKE로 쓰면 좋습니다.
;@SKILL_2010_SP_0_INVOKE_TEXT(발동자, 스킬, 장르)
;#DIM 발동자
;#DIM 스킬
;#DIMS 장르

;효과를 여기에 기술
@SKILL_2010_SP_0_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
; PRINTFORML "지금 바로 돌격한다!"
BATTLE_RATE_ATC:발동측 += 10
LOCAL:1 = RAND(2, 8)
FOR LOCAL, 0, LOCAL:1
	CALL DECREASE_SOLDIER(대상세력, 대상부대, ABL:발동자:무투*6, 1)
NEXT

@SKILL_2010_SP_0_EXPLANATION
RESULTS = 자신 부대의 받는 데미지를 증가시키고 적 부대에 연속 데미지를 준다.

@SKILL_2010_SP_0_CANT_TELL


@SKILL_2010_SP_0_RATE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
RETURN 1000

; #region == 총력전 ==
@총력전_스킬_2010_코스트(시전자)
#DIM 시전자 ; 코스트 당 대미지 스킬 카운터 위해서
RETURN 3

@총력전_스킬_2010_설명(시전자)
#DIM 시전자 ; 가변스킬 설명 위해서
RESULTS = 1범위 적에게 공격력 210％x3 대미지후 3턴간 회복 강화율 감소 (-35％). 전투 당 1회 체력이 20％ 이하 시 4턴 동안 불사 부여.
; RESULTS = 적에게 공격력 636％ 대미지 추가로 1턴간 자신에게 불사 부여


; 총력전 시작시 시전
@총력전_스킬_2010_초기(시전자)
#DIM 시전자
#DIMS DYNAMIC sTmp
; 카운터(무적용) 추가
CALL 총력전_카운터_추가(시전자, 시전자, 총력전_방식_피격양, 총력전_방식_지속, 1, 2010)
; 테스트 텍스트 추가
sTmp = %총력전_이름:시전자%『후퇴는 없다!』추가.
CALL 총력전_UI_텍스트_추가(sTmp, 칼라_노랑)
sTmp = 1회 체력 20％ 이하 시 4턴동안 불사 발동.
CALL 총력전_UI_텍스트_추가(sTmp, 칼라_노랑)
; UI 업데이트
CALL 총력전_UI_Main(0)
WAITANYKEY


@총력전_스킬_2010_발동(시전자)
#DIM 시전자

#DIM DYNAMIC 주목표
#DIM DYNAMIC 범위목표, 총력전_캐릭터수
#DIM DYNAMIC 대미지
#DIM DYNAMIC 공격명중
#DIM DYNAMIC iTmp, 2
#DIMS DYNAMIC sTmp, 3

; 발동 텍스트
sTmp:0 = 「지금 바로 돌격한다!」
sTmp:1 = 「총탄 따위 두렵지 않아!」
sTmp:2 = 「전방으로, 돌격!」
CALL SKILL_2010_SP_0_NAME
CALL 총력전_스킬_공통_발동텍스트(시전자, 98, RESULTS, sTmp)
; 목표 처리
CALL 총력전_시퀀스_목표선택(주목표, 총력전_선택_적군)
; 목표 없으면 스킬 해재
IF 주목표 < 0
	CALL 총력전_스킬_공통_스킬취소(시전자)
	RETURN
ENDIF
; 범위로 변경, 적팀 1 이상이 되면 수정 필요
CALL 총력전_목표_범위(범위목표, 주목표, 1)
; 대미지
대미지 = GETVAR_RAIDSTAT_SUM(시전자, "공격력") * 210
대미지 = DIVIDE_ROUND(대미지, 100)
FOR iTmp, 0, 총력전_캐릭터수
	SIF 범위목표:iTmp < 0
		BREAK
	공격명중 = 0
	FOR iTmp:1, 0, 3
		CALL 총력전_처리_피격(대미지, 범위목표:iTmp, 시전자)
		SIF RESULT == 1
			공격명중 = 1
	NEXT
	; 명중시 디버프
	IF 공격명중 == 1
		CALL 총력전_상태효과_추가_텍스트(범위목표:iTmp, 시전자, 상효_종류_치유강화, -35, 0, 총력전_방식_지속, 총력전_방식_턴, 3, 2010)
	ENDIF
NEXT
