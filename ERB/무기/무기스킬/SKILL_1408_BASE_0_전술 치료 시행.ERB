;-----------------------------------
;기본치 계산에 앞서 처리하는 것
;-----------------------------------
@SKILL_1408_BASE_0_EXIST
RETURN 1

@SKILL_1408_BASE_0_NAME
RESULTS = 전술 치료 시행

;레벨은 1-5까지
@SKILL_1408_BASE_0_LEVEL
RETURN 3

;대상 선택
@SKILL_1408_BASE_0_SETTARGET(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
;부대 대상
RETURN 1

;발동 판정
@SKILL_1408_BASE_0_CAN_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
SIF 발동부대 < 0
	RETURN 0
RETURN 1

;발동 텍스트를 오바라이드 하고 싶을 때에.
;「아무개의 스킬 발동!   00했다!」의 「00했다!」의 부분을 실장하고 싶은 경우는,
;이것이 아니고 INVOKE로 쓰면 좋습니다.
;@SKILL_1408_BASE_0_INVOKE_TEXT(발동자, 스킬, 장르)
;#DIM 발동자
;#DIM 스킬
;#DIMS 장르

;효과를 여기에 기술
@SKILL_1408_BASE_0_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
; PRINTFORML "도움이 필요하시면, 제게 맡겨주세요!"
CALL INCREASE_SOLDIER(발동세력, 발동부대, RAND(ABL:발동자:지략*15, ABL:발동자:지략*20), 1)
UNIT_TIRED_COUNT:발동세력:발동부대 = MAX(UNIT_TIRED_COUNT:발동세력:발동부대 - RAND(2, 4), 0)


@SKILL_1408_BASE_0_EXPLANATION
RESULTS = 자신 부대의 벙력을 회복하고 피로도를 줄인다.


@SKILL_1408_BASE_0_CANT_TELL


@SKILL_1408_BASE_0_RATE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
RETURN 250

; #region == 총력전 ==
@총력전_스킬_1408_코스트(시전자)
#DIM 시전자 ; 코스트 당 대미지 스킬 카운터 위해서
; #DIM 가변
RETURN 4

@총력전_스킬_1408_설명(시전자)
#DIM 시전자 ; 가변스킬 설명 위해서
; 해로운 효과 2 제거?
RESULTS = 1인에게 치유력 342％ 회복 해로운 효과 1 제거

@총력전_스킬_1408_발동(시전자)
#DIM 시전자

#DIM DYNAMIC 치유량
#DIM DYNAMIC 버프대상 ; 버프 대상
#DIM DYNAMIC iTmp
#DIMS DYNAMIC sTmp, 3

; 발동 텍스트
sTmp:0 = 「아프시다고요? 그렇다면 이것을.」
sTmp:1 = 「도움이 필요하시면, 제게 맡겨주세요!」
sTmp:2 = 「포기하지 마세요.」
CALL SKILL_1408_BASE_0_NAME
CALL 총력전_스킬_공통_발동텍스트(시전자, 16, RESULTS, sTmp)
; 목표 처리
sTmp = 치유 대상 선택
CALL 총력전_UI_텍스트_추가(sTmp, 칼라_노랑)
CALL 총력전_시퀀스_목표선택(버프대상, 총력전_선택_아군)
; 목표 없으면 스킬 해재
IF 버프대상 < 0
	CALL 총력전_스킬_공통_스킬취소(시전자)
	RETURN
ENDIF
; 회복 처리
치유량 = GETVAR_RAIDSTAT_SUM(시전자, "치유력") * 342
치유량 = DIVIDE_ROUND(치유량, 100)
CALL 총력전_처리_치유(치유량, 버프대상)
; 디버프 제거
CALL 총력전_상태효과_제거_디버프(버프대상)
