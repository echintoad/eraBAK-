;-----------------------------------
;기본치 계산에 앞서 처리하는 것
;-----------------------------------
@SKILL_1701_SP_0_EXIST
RETURN 1

@SKILL_1701_SP_0_NAME
RESULTS = 밸런스 붕괴

;레벨은 1-5까지
@SKILL_1701_SP_0_LEVEL
RETURN 5

;대상 선택
@SKILL_1701_SP_0_SETTARGET(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
;부대 대상
RETURN 1

;발동 판정
@SKILL_1701_SP_0_CAN_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
RETURN 1

;발동 텍스트를 오바라이드 하고 싶을 때에.
;「아무개의 스킬 발동!   00했다!」의 「00했다!」의 부분을 실장하고 싶은 경우는,
;이것이 아니고 INVOKE로 쓰면 좋습니다.
;@SKILL_1701_SP_0_INVOKE_TEXT(발동자, 스킬, 장르)
;#DIM 발동자
;#DIM 스킬
;#DIMS 장르

;효과를 여기에 기술
@SKILL_1701_SP_0_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
; PRINTFORML "이 빛에 의지를 담아… 꿰뚫어라! 밸런스 붕괴!"
CALL DECREASE_SOLDIER(대상세력, 대상부대, (BATTLE_무투파워:발동측:발동번호 / 10), 1)


@SKILL_1701_SP_0_EXPLANATION
RESULTS = 적에게 아군 무투 합에 비례한 데미지를 준다.


@SKILL_1701_SP_0_CANT_TELL

; 아이가 배울수 없도록 한다
@SKILL_1701_SP_0_CANT_INHERIT


@SKILL_1701_SP_0_RATE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
RETURN 100

; #region == 총력전 ==
@총력전_스킬_1701_코스트(시전자)
#DIM 시전자 ; 코스트 당 대미지 스킬 카운터 위해서
; #DIM 가변
RETURN 6

; 기능 테스트, 기본 공격당 대미지, 치명타 증가로 변경
@총력전_스킬_1701_설명(시전자)
#DIM 시전자 ; 가변스킬 설명 위해서
; RESULTS = 기본 공격 2회 마다 1 충전, 최대 2 충전. 스킬 시전시 치명타 22 + (12 x 충전수) 증가후 모든 적에게 공격력 500％ + (250％ x 충전수) 대미지, 이후 충전 모두 소비.
RESULTS = 스킬 시전시 치명타 22 + (12 x 충전수) 증가후 모든 적에게 공격력 500％ + (250％ x 충전수) 대미지, 이후 충전 모두 소비. 충전은 기본 공격 2회 마다 1 충전, 최대 2 충전. 
; RESULTS = 기본 공격 2회 마다 1 충전, 최대 2 충전. 스킬 시전시 모든 적에게 공격력 500％ + (250％ x 충전수) 대미지와 3턴 동안 치명타 22 + (12 x 충전수) 증가, 이후 충전 모두 소비.
; RESULTS = 1턴간 자신의 치명타확률 22％ 증가 추가로 적에게 공격력 900％ 대미지 발동 횟수에 따라 최대 2배까지 증가한다


; 총력전 시작시 시전
; 에너지
@총력전_스킬_1701_초기(시전자)
#DIM 시전자
; 카운터(에너지) 추가
CALL 총력전_카운터_추가(시전자, 시전자, 총력전_방식_공격, 총력전_방식_지속, 1, 1701)


@총력전_스킬_1701_발동(시전자)
#DIM 시전자

#DIM DYNAMIC 목표, 총력전_캐릭터수
#DIM DYNAMIC 대미지
#DIM DYNAMIC 치명증가
#DIM DYNAMIC 에너지
#DIM DYNAMIC 대사번호
#DIM DYNAMIC iTmp
#DIMS DYNAMIC sTmp

; 발동 텍스트
CALL SKILL_1701_SP_0_NAME
sTmp = %총력전_이름:시전자% 스킬 시전:『%RESULTS%』
CALL 총력전_UI_텍스트_추가(sTmp, 칼라_노랑)
iTmp = 총력전_ID:시전자
IF iTmp >= 0 && NO:iTmp == 32
	; 아리스일 경우
	대사번호 = RAND:3
	SELECTCASE 대사번호
		CASE 0
			sTmp = 「마력 충전 100％…」
		CASE 1
			sTmp = 「에너지 과부하…」
		CASEELSE
			sTmp = 「이 빛에 의지를 담아, 꿰뚫어라!」
	ENDSELECT
	CALL 총력전_UI_텍스트_추가(sTmp)
ENDIF
; 목표 처리
CALL 총력전_목표_전체(목표, 1)
; 대미지
대미지 = GETVAR_RAIDSTAT_SUM(시전자, "공격력") * 432
대미지 = DIVIDE_ROUND(대미지, 100)
; 에너지 소비
CALL 총력전_카운터_검색_스킬번호(시전자, 1701)
iTmp = RESULT
에너지 = 총력전_카운터_증가양:시전자:iTmp / 2
[IF_DEBUG]
sTmp = 에너지 카운터양 = {총력전_카운터_증가양:시전자:iTmp}, 에너지 = {에너지}
CALL 총력전_UI_텍스트_추가(sTmp, 칼라_파노랑)
[ENDIF]
총력전_카운터_증가양:시전자:iTmp = 0
; 치명타 증가
치명증가 = 22 + 에너지 * 12
CALL 총력전_상태효과_추가_텍스트(시전자, 시전자, 상효_종류_치명타, 치명증가, 1, 총력전_방식_스킬, 총력전_방식_스킬, 1, 1701)
; CALL 총력전_상태효과_스탯처리(시전자, 총력전_방식_스킬)
; ; 버프 텍스트 추가
; sTmp = %조사처리(총력전_이름:시전자, "의")% 치명타 ({치명증가}) 증가!
; CALL 총력전_UI_텍스트_추가(sTmp, 칼라_노랑)
; ; UI 업데이트
; CALL 총력전_UI_Main(0)
; WAITANYKEY
; 추가 텍스트
iTmp = 총력전_ID:시전자
IF iTmp >= 0 && NO:iTmp == 32
	SELECTCASE 대사번호
		CASE 0
			sTmp = 「…갑니다!」
		CASE 1
			sTmp = 「빛이여!」
		CASEELSE
			sTmp = 「밸런스 붕괴!!」
	ENDSELECT
	CALL 총력전_UI_텍스트_추가(sTmp)
	; UI 업데이트
	CALL 총력전_UI_Main(0)
	WAITANYKEY
ENDIF
; 대미지 증가
iTmp = 에너지 * GETVAR_RAIDSTAT_SUM(시전자, "공격력") * 250
대미지 += DIVIDE_ROUND(iTmp, 100)
FOR iTmp, 0, 총력전_캐릭터수
	SIF 목표:iTmp < 0
		BREAK
	CALL 총력전_처리_피격(대미지, 목표:iTmp, 시전자)
NEXT

; #region == 카운터 ==
; 에너지
@총력전_스킬_1701_0_카운터_증가(캐릭터, 대상, 카운터번호, 증가양)
#DIM 캐릭터
#DIM 대상
#DIM 카운터번호
#DIM 증가양

#DIM DYNAMIC iTmp
#DIMS DYNAMIC sTmp

; 최대 충전이면
SIF 총력전_카운터_증가양:캐릭터:카운터번호 >= 4
	RETURN
; 충전
iTmp = 총력전_카운터_증가양:캐릭터:카운터번호
총력전_카운터_증가양:캐릭터:카운터번호 = MIN(iTmp + 증가양, 4)
; 충전 시 (2) 메세지 추가
IF 총력전_카운터_증가양:캐릭터:카운터번호 % 2 == 0
	iTmp = 총력전_ID:캐릭터
	IF iTmp >= 0 && NO:iTmp == 32
		; 아리스일 경우
		SELECTCASE RAND:2
			CASE 0
				sTmp = 「충전합니다!」
			CASEELSE
				sTmp = 「마력충전!」
		ENDSELECT
		CALL 총력전_UI_텍스트_추가(sTmp)
	ENDIF
	iTmp = 총력전_카운터_증가양:캐릭터:카운터번호 / 2
	sTmp = %조사처리(총력전_이름:캐릭터, "의")%『에너지』충전 ({iTmp})
	CALL 총력전_UI_텍스트_추가(sTmp)
	; UI 업데이트
	CALL 총력전_UI_Main(0)
	WAITANYKEY
ENDIF