;-----------------------------------
;기본치 계산에 앞서 처리하는 것
;-----------------------------------
@SKILL_1111_SP_0_EXIST
RETURN 1

@SKILL_1111_SP_0_NAME
RESULTS = 참 잘했어요

;레벨은 1-5까지
@SKILL_1111_SP_0_LEVEL
RETURN 3

;대상 선택
@SKILL_1111_SP_0_SETTARGET(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
;부대 대상
RETURN 1

;발동 판정
@SKILL_1111_SP_0_CAN_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
RETURN 1


;발동 텍스트를 오바라이드 하고 싶을 때에.
;「아무개의 스킬 발동!   00했다!」의 「00했다!」의 부분을 실장하고 싶은 경우는,
;이것이 아니고 INVOKE로 쓰면 좋습니다.
;@SKILL_1111_SP_0_INVOKE_TEXT(발동자, 스킬, 장르)
;#DIM 발동자
;#DIM 스킬
;#DIMS 장르

;효과를 여기에 기술
@SKILL_1111_SP_0_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
; PRINTFORML "백점만점, 참 잘했어요!"
CALL INCREASE_SOLDIER(발동세력, 발동부대, ABL:발동자:지략*6, 1)
BATTLE_SKILL_RATE:발동측:발동번호 += 25


@SKILL_1111_SP_0_EXPLANATION
RESULTS = 자신 부대를 회복시키고 자신의 스킬 발동 확률을 증가시킨다.

@SKILL_1111_SP_0_CANT_TELL

; 아이가 배울수 없도록 한다
@SKILL_1111_SP_0_CANT_INHERIT


@SKILL_1111_SP_0_RATE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
RETURN 400

; #region == 총력전 ==
@총력전_스킬_1111_코스트(시전자)
#DIM 시전자 ; 코스트 당 대미지 스킬 카운터 위해서
; #DIM 가변
RETURN 2

; 카운터 추가
@총력전_스킬_1111_초기(시전자)
#DIM 시전자
; 크리 카운터
CALL _총력전_스킬_1111_카운터추가(시전자)
; 칭찬 도장 카운터
CALL 총력전_카운터_추가(시전자, 시전자, 총력전_방식_지속, 총력전_방식_지속, 1, 1111, 1)
; 일단은 턴 종료시 카운터 추가
; 나중에 소환시 작동하는 함수 있으면 거기서 처리?
CALL 총력전_카운터_추가(시전자, 시전자, 총력전_방식_턴, 총력전_방식_지속, 1, 1111, 2)


@총력전_스킬_1111_설명(시전자)
#DIM 시전자 ; 가변스킬 설명 위해서
; 결합 치명타 == combined critical hit???
RESULTS = 아군 1인에게 치유력 250％ 회복, 회복 대상이 자신이 아닐경우 자신에게 치유력 150％ 회복,『칭찬 도장』1개 적립. 아군 결합 치명타 10회 성공시 치유력 100％ 회복, 『칭찬 도장』1개 적립. 『칭찬 도장』3개 적립 시 자신을 제외한 아군 스킬 코스트 1 감소.
; RESULTS = 2인에게 치유력 290％ 회복 추가로 1회까지 대상의 스킬 코스트 1 감소


@총력전_스킬_1111_발동(시전자)
#DIM 시전자

#DIM DYNAMIC 치유량
#DIM DYNAMIC 치유대상
#DIM DYNAMIC iTmp
#DIMS DYNAMIC sTmp, 3

; 발동 텍스트
sTmp:0 = 「시험지 돌려드릴게요~!」
sTmp:1 = 「참 잘했어요!」
sTmp:2 = 「백점만점, 참 잘했어요!」
CALL SKILL_1111_SP_0_NAME
CALL 총력전_스킬_공통_발동텍스트(시전자, 92, RESULTS, sTmp)
; 목표 처리
sTmp = 치유 대상 선택
CALL 총력전_UI_텍스트_추가(sTmp, 칼라_노랑)
CALL 총력전_시퀀스_목표선택(치유대상, 총력전_선택_아군)
; 목표 없으면 스킬 해재
IF 치유대상 < 0
	CALL 총력전_스킬_공통_스킬취소(시전자)
	RETURN
ENDIF
; 치유
치유량 = GETVAR_RAIDSTAT_SUM(시전자, "치유력") * 250
치유량 = DIVIDE_ROUND(치유량, 100)
CALL 총력전_처리_치유(치유량, 치유대상, 시전자)
; 시전자가 치유 대상 아닐 경우 자가 치유
IF 치유대상 != 시전자
	치유량 = GETVAR_RAIDSTAT_SUM(시전자, "치유력") * 150
	치유량 = DIVIDE_ROUND(치유량, 100)
	CALL 총력전_처리_치유(치유량, 시전자, 시전자)
	; 칭찬 도장 추가
	CALL 총력전_카운터_검색_스킬번호(시전자, 1111, 1)
	CALL 총력전_스킬_1111_1_카운터_증가(시전자, -1, RESULT, 1)
ENDIF


; #region == 카운터 ==
; 치명 카운터
@총력전_스킬_1111_0_카운터_증가(캐릭터, 대상, 카운터번호, 증가양)
#DIM 캐릭터
#DIM 대상
#DIM 카운터번호
#DIM 증가양

#DIM DYNAMIC 치유량
#DIM DYNAMIC 카운터시전자
#DIM DYNAMIC 시전자카운터번호

카운터시전자 = 총력전_카운터_시전자:캐릭터:카운터번호
; 카운터 시전자면 무시
IF 카운터시전자 == 캐릭터
	RETURN
ENDIF
CALL 총력전_카운터_검색_스킬번호(카운터시전자, 1111, 0)
시전자카운터번호 = RESULT
; 시전자 카운터로 집중/증가
총력전_카운터_증가양:카운터시전자:시전자카운터번호 += 증가양
IF 총력전_카운터_증가양:카운터시전자:시전자카운터번호 >= 10
	총력전_카운터_증가양:카운터시전자:시전자카운터번호 = 0
	; 카운터 10 일 경우 치유
	치유량 = GETVAR_RAIDSTAT_SUM(카운터시전자, "치유력") * 100
	치유량 = DIVIDE_ROUND(치유량, 100)
	CALL 총력전_처리_치유(치유량, 캐릭터, 카운터시전자)
	; 칭찬 도장 추가
	CALL 총력전_카운터_검색_스킬번호(카운터시전자, 1111, 1)
	CALL 총력전_스킬_1111_1_카운터_증가(카운터시전자, -1, RESULT, 1)
ENDIF

; 칭찬 도장 카운터
; 아직 1111 스킬 쓰는 캐릭터가 1명 이상일때 처리방법 없음...
; 카운터 0 증가시 회복력 가장 높은 스킬 1111 사용자 찾기?, 도장 카운터는 양쪽 다 올리고?
@총력전_스킬_1111_1_카운터_증가(캐릭터, 대상, 카운터번호, 증가양)
#DIM 캐릭터
#DIM 대상
#DIM 카운터번호
#DIM 증가양

#DIM DYNAMIC 버프대상, 총력전_캐릭터수
#DIM DYNAMIC iTmp
#DIMS DYNAMIC sTmp

총력전_카운터_증가양:캐릭터:카운터번호 += 증가양
sTmp = %총력전_이름:캐릭터%『칭찬 도장』({총력전_카운터_증가양:캐릭터:카운터번호}/3)
CALL 총력전_UI_텍스트_추가(sTmp, 칼라_노랑)
; UI 업데이트
CALL 총력전_UI_Main(0)
WAITANYKEY

IF 총력전_카운터_증가양:캐릭터:카운터번호 >= 3
	총력전_카운터_증가양:캐릭터:카운터번호 = 0
	; 목표 처리
	CALL 총력전_목표_전체(버프대상, 0)
	FOR iTmp, 0, 총력전_캐릭터수
		SIF 버프대상:iTmp < 0
			BREAK
		; 시전자 빼고 코스트 감소
		SIF 버프대상:iTmp == 캐릭터
			CONTINUE
		CALL 총력전_상태효과_추가_텍스트(버프대상:iTmp, 캐릭터, 상효_종류_코스트감소, 1, 1, 총력전_방식_지속, 총력전_방식_스킬, 1, 1111)
	NEXT
ENDIF

; 소환물 등 나중에 추가된 캐릭터에 크리 카운터 추가
@총력전_스킬_1111_2_카운터_증가(캐릭터, 대상, 카운터번호, 증가양)
#DIM 캐릭터
#DIM 대상
#DIM 카운터번호
#DIM 증가양
CALL _총력전_스킬_1111_카운터추가(캐릭터)


; #region == 개인 함수? ==
; 에라엔 private function 이 없는걸로 알지만...
; 크리티컬 카운터 추가, 소환수 있으면 추가 할수 있게
@_총력전_스킬_1111_카운터추가(시전자)
#DIM 시전자

#DIM DYNAMIC 대상, 총력전_캐릭터수
#DIM DYNAMIC iTmp
; 목표 처리
CALL 총력전_목표_전체(대상, 0)

FOR iTmp, 0, 총력전_캐릭터수
	SIF 대상:iTmp < 0
		BREAK
	; 아군만 적용
	; SIF 대상:iTmp == 시전자
	; 	CONTINUE
	CALL 총력전_카운터_추가(대상:iTmp, 시전자, 총력전_방식_치명타, 총력전_방식_지속, 1, 1111, 0)
NEXT