;-----------------------------------
;기본치 계산에 앞서 처리하는 것
;-----------------------------------
@SKILL_2118_BASE_0_EXIST
RETURN 1

@SKILL_2118_BASE_0_NAME
RESULTS = 신기묘산의 한 수

;레벨은 1-5까지
@SKILL_2118_BASE_0_LEVEL
RETURN 5

;대상 선택
@SKILL_2118_BASE_0_SETTARGET(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
;부대 대상
RETURN 1

;발동 판정
@SKILL_2118_BASE_0_CAN_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
RETURN 1

;발동 텍스트를 오바라이드 하고 싶을 때에.
;「아무개의 스킬 발동!   00했다!」의 「00했다!」의 부분을 실장하고 싶은 경우는,
;이것이 아니고 INVOKE로 쓰면 좋습니다.
;@SKILL_2118_BASE_0_INVOKE_TEXT(발동자, 스킬, 장르)
;#DIM 발동자
;#DIM 스킬
;#DIMS 장르

;효과를 여기에 기술
@SKILL_2118_BASE_0_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
PRINTFORML 적 부대의 방어력이 감소했다!
TIMES BATTLE_DEF:대상측, 0.80
CALL DECREASE_SOLDIER(대상세력, 대상부대, RAND(ABL:발동자:지략*10 , ABL:발동자:지략*15), 1)




@SKILL_2118_BASE_0_EXPLANATION
RESULTS = 적 부대에 데미지를 주고 방어력을 감소시킨다.


@SKILL_2118_BASE_0_CANT_TELL


@SKILL_2118_BASE_0_RATE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
RETURN 400

; #region == 총력전 ==
@총력전_스킬_2118_코스트(시전자)
#DIM 시전자 ; 코스트 당 대미지 스킬 카운터 위해서
RETURN 5

@총력전_스킬_2118_설명(시전자)
#DIM 시전자 ; 가변스킬 설명 위해서
; 아님 모든 적에게?
RESULTS = 2범위 적에게 대미지 (공격력 160％x3), 3턴간 적의 방어력 감소 (-28％),『풍림화산』 3개면 자신을 제외한 아군 코스트 1 감소. 2턴마다 1턴동안 공격력 증가 (+20％),『풍림화산』추가. 『풍림화산』최대 3개.
; RESULTS = 3턴간 적의 방어력 28％ 감소 자신의 공격력 32％ 증가 추가로 적에게 공격력 480％ 대미지 2회 사용시 1회까지 아군 전체의 스킬 코스트 1감소

; 총력전 시작시 시전
@총력전_스킬_2118_초기(시전자)
#DIM 시전자
; 풍림화산 추가
CALL 총력전_카운터_추가(시전자, 시전자, 총력전_방식_지속, 총력전_방식_지속, 1, 2118, 0)
; 턴 카운터 추가
CALL 총력전_카운터_추가(시전자, 시전자, 총력전_방식_턴, 총력전_방식_지속, 1, 2118, 1)

@총력전_스킬_2118_발동(시전자)
#DIM 시전자

#DIM DYNAMIC 주목표
#DIM DYNAMIC 범위목표, 총력전_캐릭터수
#DIM DYNAMIC 대미지
#DIM DYNAMIC 공격명중
#DIM DYNAMIC iTmp, 2
#DIMS DYNAMIC sTmp, 3

; 발동 텍스트
sTmp:0 = 「다음을 위한 수는…」
sTmp:1 = 「전황을 뒤흔들 한 수를.」
sTmp:2 = 「백화요란의 힘, 듬뿍 맛보도록 해.」
CALL SKILL_2118_BASE_0_NAME
CALL 총력전_스킬_공통_발동텍스트(시전자, 138, RESULTS, sTmp)
; 목표 처리
CALL 총력전_시퀀스_목표선택(주목표, 총력전_선택_적군)
; 목표 없으면 스킬 해재
IF 주목표 < 0
	CALL 총력전_스킬_공통_스킬취소(시전자)
	RETURN
ENDIF
; 범위로 변경, 적팀 1 이상이 되면 수정 필요
CALL 총력전_목표_범위(범위목표, 주목표, 1, 2)
; 대미지
대미지 = GETVAR_RAIDSTAT_SUM(시전자, "공격력") * 160
대미지 = DIVIDE_ROUND(대미지, 100)
FOR iTmp, 0, 총력전_캐릭터수
	SIF 범위목표:iTmp < 0
		BREAK
    공격명중 = 0
	FOR iTmp:1, 0, 3
		CALL 총력전_처리_피격(대미지, 범위목표:iTmp, 시전자)
        SIF RESULT == 1
            공격명중 = 1
	NEXT
    IF 공격명중 == 1
		CALL 총력전_상태효과_추가_텍스트(범위목표:iTmp, 시전자, 상효_종류_방어력백분율, -38, 0, 총력전_방식_지속, 총력전_방식_턴, 3, 2118, 0)
    ENDIF
NEXT
; 『풍림화산』
CALL 총력전_카운터_검색_스킬번호(시전자, 2118)
iTmp = RESULT
IF 총력전_카운터_증가양:시전자:iTmp >= 3
    총력전_카운터_증가양:시전자:iTmp = 0
    ; 전 아군 선택
    CALL 총력전_목표_전체(범위목표, 0)
    FOR iTmp, 0, 총력전_캐릭터수
    	SIF 범위목표:iTmp < 0
		    BREAK
        ; 시전자 제외
        SIF 범위목표:iTmp == 시전자
            CONTINUE
		CALL 총력전_상태효과_추가_텍스트(범위목표:iTmp, 시전자, 상효_종류_코스트감소, 1, 1, 총력전_방식_지속, 총력전_방식_스킬, 1, 2118, 1)
        
    NEXT
ENDIF


; #region == 카운터 ==
; 풍림화산
@총력전_스킬_2118_0_카운터_증가(캐릭터, 대상, 카운터번호, 증가양)
#DIM 캐릭터
#DIM 대상
#DIM 카운터번호
#DIM 증가양

; #DIM DYNAMIC iTmp
#DIMS DYNAMIC sTmp

IF 총력전_카운터_증가양:캐릭터:카운터번호 < 3
    총력전_카운터_증가양:캐릭터:카운터번호 += 증가양
    sTmp = %총력전_이름:캐릭터%『풍림화산』({총력전_카운터_증가양:캐릭터:카운터번호}/3)
    CALL 총력전_UI_텍스트_추가(sTmp, 칼라_노랑)
    ; UI 업데이트
    CALL 총력전_UI_Main(0)
    WAITANYKEY
    ; 공격력 증가
    CALL 총력전_상태효과_추가_텍스트(캐릭터, 캐릭터, 상효_종류_공격력백분율, 20, 1, 총력전_방식_지속, 총력전_방식_턴, 2, 2118, 2)
ENDIF

; 턴 카운터
@총력전_스킬_2118_1_카운터_증가(캐릭터, 대상, 카운터번호, 증가양)
#DIM 캐릭터
#DIM 대상
#DIM 카운터번호
#DIM 증가양

총력전_카운터_증가양:캐릭터:카운터번호 += 증가양
IF 총력전_카운터_증가양:캐릭터:카운터번호 >= 2
    총력전_카운터_증가양:캐릭터:카운터번호 = 0
    ; 풍림화산 추가
	CALL 총력전_카운터_검색_스킬번호(캐릭터, 2118, 0)
	CALL 총력전_스킬_2118_0_카운터_증가(캐릭터, -1, RESULT, 1)
ENDIF
