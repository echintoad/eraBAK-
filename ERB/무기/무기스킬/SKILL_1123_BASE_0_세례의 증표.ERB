;-----------------------------------
;기본치 계산에 앞서 처리하는 것
;-----------------------------------
@SKILL_1123_BASE_0_EXIST
RETURN 1

@SKILL_1123_BASE_0_NAME
RESULTS = 세례의 증표

;레벨은 1-5까지
@SKILL_1123_BASE_0_LEVEL
RETURN 4

;대상 선택
@SKILL_1123_BASE_0_SETTARGET(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
;부대 대상
RETURN 1

;발동 판정
@SKILL_1123_BASE_0_CAN_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
RETURN 1

;발동 텍스트를 오바라이드 하고 싶을 때에.
;「아무개의 스킬 발동!   00했다!」의 「00했다!」의 부분을 실장하고 싶은 경우는,
;이것이 아니고 INVOKE로 쓰면 좋습니다.
;@SKILL_1123_BASE_0_INVOKE_TEXT(발동자, 스킬, 장르)
;#DIM 발동자
;#DIM 스킬
;#DIMS 장르

;효과를 여기에 기술
@SKILL_1123_BASE_0_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
; PRINTFORML "이 탄환에 축복을...!"
TIMES BATTLE_ATK:발동측, 1.3
TIMES BATTLE_INT:발동측, 1.3

@SKILL_1123_BASE_0_EXPLANATION
RESULTS = 자신 부대의 무투와 지략을 증가시킨다.

@SKILL_1123_BASE_0_CANT_TELL


@SKILL_1123_BASE_0_RATE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
RETURN 200

; #region == 총력전 ==
@총력전_스킬_1123_코스트(시전자)
#DIM 시전자 ; 코스트 당 대미지 스킬 카운터 위해서
; #DIM 가변
RETURN 4

; alt.opt.1 공속 증가?
; add.note. 일단은 시스템 성능 확인때문에 이렇게 하는데, 나중에 한번 화인해봐야 할듯.
; 지금 사쿠라코랑 시스터후드 가 맞지 않는거 같고...
; 구호 기사단(미네)이랑 정의실현부(하스미)랑도 그렇다할 맞는 스킬이 없는 느낌
@총력전_스킬_1123_설명(시전자)
#DIM 시전자 ; 가변스킬 설명 위해서
RESULTS = 적에게 공격력 625％ 대미지, 적에게 3턴간 도발 부여. 시전자가 스킬 시전시 공격력 50％ 기록, 3턴간 기본 공격시 추가 대미지
; RESULTS = 3턴간 받는 모든 피해에 18％ 추가 대미지 추가로 적에게 공격력 625％ 대미지

@총력전_스킬_1123_발동(시전자)
#DIM 시전자

#DIM DYNAMIC 목표
#DIM DYNAMIC 대미지
#DIM DYNAMIC iTmp
#DIMS DYNAMIC sTmp, 3

; 발동 텍스트
sTmp:0 = 「이 탄환에 축복을...!」
sTmp:1 = 「저희에게 은혜를 베푸소서.」
sTmp:2 = 「은총에 감사를.」
CALL SKILL_1123_BASE_0_NAME
CALL 총력전_스킬_공통_발동텍스트(시전자, 72, RESULTS, sTmp)
; 목표 처리
CALL 총력전_시퀀스_목표선택(목표, 총력전_선택_적군)
; 목표 없으면 스킬 해재
IF 목표 < 0
	CALL 총력전_스킬_공통_스킬취소(시전자)
	RETURN
ENDIF
; 대미지
대미지 = GETVAR_RAIDSTAT_SUM(시전자, "공격력") * 625
대미지 = DIVIDE_ROUND(대미지, 100)
CALL 총력전_처리_피격(대미지, 목표, 시전자)
; 도발 부여, 텍스트 없어도 될거 같기도 하고...
CALL 총력전_상태효과_추가_텍스트(목표, 시전자, 상효_종류_도발, 1, 2, 총력전_방식_지속, 총력전_방식_턴, 3, 1123)
; 추가 공격 카운터
CALL 총력전_카운터_추가(시전자, 시전자, 총력전_방식_공격, 총력전_방식_턴, 3, 1123)
; 공격 증가양에(안 쓰니까) 기록
; 기능 시험차 이렇게, 나중에 할거면 공격 할때마다 계산
대미지 = GETVAR_RAIDSTAT_SUM(시전자, "공격력") * 50
대미지 = DIVIDE_ROUND(대미지, 100)
CALL 총력전_카운터_검색_스킬번호(시전자, 1123)
iTmp = RESULT
총력전_카운터_증가양:시전자:iTmp = 대미지

; #region == 카운터 ==
; 기본 공격시 증가량 비례 추가 공격
@총력전_스킬_1123_0_카운터_증가(캐릭터, 대상, 카운터번호, 증가양)
#DIM 캐릭터
#DIM 대상
#DIM 카운터번호
#DIM 증가양

#DIMS DYNAMIC sTmp
sTmp = 『세례의 증표』추가 대미지!
CALL 총력전_UI_텍스트_추가(sTmp, 칼라_노랑)
CALL 총력전_처리_피격(총력전_카운터_증가양:캐릭터:카운터번호, 대상, 캐릭터)
