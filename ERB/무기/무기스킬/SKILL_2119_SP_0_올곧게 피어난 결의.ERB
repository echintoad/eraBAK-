;-----------------------------------
;기본치 계산에 앞서 처리하는 것
;-----------------------------------
@SKILL_2119_SP_0_EXIST
RETURN 1

@SKILL_2119_SP_0_NAME
RESULTS = 올곧게 피어난 결의

;레벨은 1-5까지
@SKILL_2119_SP_0_LEVEL
RETURN 4

;대상 선택
@SKILL_2119_SP_0_SETTARGET(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
;부대 대상
RETURN 1

;발동 판정
@SKILL_2119_SP_0_CAN_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
RETURN 1

;발동 텍스트를 오바라이드 하고 싶을 때에.
;「아무개의 스킬 발동!   00했다!」의 「00했다!」의 부분을 실장하고 싶은 경우는,
;이것이 아니고 INVOKE로 쓰면 좋습니다.
;@SKILL_2119_SP_0_INVOKE_TEXT(발동자, 스킬, 장르)
;#DIM 발동자
;#DIM 스킬
;#DIMS 장르

;효과를 여기에 기술
@SKILL_2119_SP_0_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측

CALL DECREASE_SOLDIER(대상세력, 대상부대,RAND(ABL:발동자:무투*20 , ABL:발동자:무투*30), 1)

@SKILL_2119_SP_0_EXPLANATION
RESULTS = 적 부대에 데미지를 준다.


@SKILL_2119_SP_0_CANT_TELL

; 아이가 배울수 없도록 한다
@SKILL_2119_SP_0_CANT_INHERIT


@SKILL_2119_SP_0_RATE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
RETURN 800

; #region == 총력전 ==
@총력전_스킬_2119_코스트(시전자)
#DIM 시전자 ; 코스트 당 대미지 스킬 카운터 위해서
RETURN 7


@총력전_스킬_2119_설명(시전자)
#DIM 시전자 ; 가변스킬 설명 위해서
RESULTS = 1범위 적에게 대미지 (공격력 200％),『기원』5개당 추가 확정 치명 대미지 (공격력 100％). 2턴마다『기원』5 획득. 자신을 제외한 아군이 소모한 코스트만큼『기원』획득. 『기원』최대 25.
; RESULTS = 모든 기원을 소모하여 기원 1당 적에게 공격력 125％ 확정 치명 대미지 아군이 소모한 코스트만큼 기원을 획득

; 총력전 시작시 시전
; 스쿼드 멤버당 공격력 증가, 사오리는 치명 대미지로 변경
@총력전_스킬_2119_초기(시전자)
#DIM 시전자

#DIM DYNAMIC 대상, 총력전_캐릭터수
#DIM DYNAMIC iTmp
; #DIMS DYNAMIC sTmp

; 아군에 기원 스킬 카운터 추가
CALL 총력전_목표_전체(대상, 0)
FOR iTmp, 0, 총력전_캐릭터수
	SIF 대상:iTmp < 0
		BREAK
	; 시전자면 스킵
	SIF 대상:iTmp == 시전자
		CONTINUE
	CALL 총력전_카운터_추가(대상:iTmp, 시전자, 총력전_방식_스킬, 총력전_방식_지속, 1, 2119, 1)
NEXT
; 2턴 기원 추가 획득 카운터 추가
CALL 총력전_카운터_추가(시전자, 시전자, 총력전_방식_턴, 총력전_방식_지속, 1, 2119, 2)
; 기원 카운터 추가
CALL 총력전_카운터_추가(시전자, 시전자, 총력전_방식_지속, 총력전_방식_지속, 1, 2119, 0)


@총력전_스킬_2119_발동(시전자)
#DIM 시전자

#DIM DYNAMIC 주목표
#DIM DYNAMIC 범위목표, 총력전_캐릭터수
#DIM DYNAMIC 대미지
#DIM DYNAMIC 기원공격회수
#DIM DYNAMIC iTmp, 2
#DIMS DYNAMIC sTmp, 3

; 발동 텍스트
sTmp:0 = 「다른 누구도 아닌, 제 손으로…」
sTmp:1 = 「저의 진심을 담아!」
sTmp:2 = 「소녀의 전력을, 보여드리겠사와요!」
CALL SKILL_2119_SP_0_NAME
CALL 총력전_스킬_공통_발동텍스트(시전자, 139, RESULTS, sTmp)
; 목표 처리
CALL 총력전_시퀀스_목표선택(주목표, 총력전_선택_적군)
; 목표 없으면 스킬 해재
IF 주목표 < 0
	CALL 총력전_스킬_공통_스킬취소(시전자)
	RETURN
ENDIF
; 범위로 변경, 적팀 1 이상이 되면 수정 필요
CALL 총력전_목표_범위(범위목표, 주목표, 1, 2)
; 대미지
대미지 = GETVAR_RAIDSTAT_SUM(시전자, "공격력") * 200
대미지 = DIVIDE_ROUND(대미지, 100)
FOR iTmp, 0, 총력전_캐릭터수
	SIF 범위목표:iTmp < 0
		BREAK
	CALL 총력전_처리_피격(대미지, 범위목표:iTmp, 시전자)
NEXT
; 기원 회수 처리
CALL 총력전_카운터_검색_스킬번호(시전자, 2119, 0)
iTmp = RESULT
기원공격회수 = 총력전_카운터_증가양:시전자:iTmp / 5
; 남은 기원 텍스트 뜨게
iTmp:1 = 총력전_카운터_증가양:시전자:iTmp % 5
총력전_카운터_증가양:시전자:iTmp = 0
CALL 총력전_스킬_2119_0_카운터_증가(시전자, -1, iTmp, iTmp:1)
; 기원 대미지
대미지 = GETVAR_RAIDSTAT_SUM(시전자, "공격력") * 100
대미지 = DIVIDE_ROUND(대미지, 100)
FOR iTmp, 0, 총력전_캐릭터수
	SIF 범위목표:iTmp < 0
		BREAK
	FOR iTmp:1, 0, 기원공격회수
		CALL 총력전_처리_피격(대미지, 범위목표:iTmp, 시전자, 1, 2)
	NEXT
NEXT

; #DIM 대미지
; 대미지 = (총력전_공격력:LOCAL * 125 / 100) * 기원 * (100 - 총력전_보스_방어력) / 100
; 기원 = 0
; CALL 공격판정처리(대미지,LOCAL,1)


; #region == 카운터 ==
; 기원
@총력전_스킬_2119_0_카운터_증가(캐릭터, 대상, 카운터번호, 증가양)
#DIM 캐릭터
#DIM 대상
#DIM 카운터번호
#DIM 증가양

#DIMS DYNAMIC sTmp

IF 총력전_카운터_증가양:캐릭터:카운터번호 < 25
	총력전_카운터_증가양:캐릭터:카운터번호 = MIN(총력전_카운터_증가양:캐릭터:카운터번호 + 증가양, 25)
	sTmp = %총력전_이름:캐릭터%『기원』({총력전_카운터_증가양:캐릭터:카운터번호}/25)
	CALL 총력전_UI_텍스트_추가(sTmp, 칼라_노랑)
	; UI 업데이트
	CALL 총력전_UI_Main(0)
	WAITANYKEY
ENDIF

; 아군 스킬 코스트 카운터
@총력전_스킬_2119_1_카운터_증가(캐릭터, 대상, 카운터번호, 증가양)
#DIM 캐릭터
#DIM 대상
#DIM 카운터번호
#DIM 증가양

#DIM DYNAMIC 카운터시전자
#DIM DYNAMIC 시전자카운터번호

; 시전자 정보
카운터시전자 = 총력전_카운터_시전자:캐릭터:카운터번호
CALL 총력전_카운터_검색_스킬번호(카운터시전자, 2119, 0)
시전자카운터번호 = RESULT
; 시전자 기원 카운터에 스킬 코스트 추가
CALL 총력전_스킬_2119_0_카운터_증가(카운터시전자, -1, 시전자카운터번호, 총력전_스킬_코스트:캐릭터)

; 2턴 기원 추가
@총력전_스킬_2119_2_카운터_증가(캐릭터, 대상, 카운터번호, 증가양)
#DIM 캐릭터
#DIM 대상
#DIM 카운터번호
#DIM 증가양

#DIMS DYNAMIC iTmp

총력전_카운터_증가양:캐릭터:카운터번호 += 증가양
IF 총력전_카운터_증가양:캐릭터:카운터번호 >= 2
	총력전_카운터_증가양:캐릭터:카운터번호 = 0
	CALL 총력전_카운터_검색_스킬번호(캐릭터, 2119, 0)
	CALL 총력전_스킬_2119_0_카운터_증가(캐릭터, -1, RESULT, 5)
ENDIF