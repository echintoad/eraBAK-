;-----------------------------------
;기본치 계산에 앞서 처리하는 것
;-----------------------------------
@SKILL_1504_SP_0_EXIST
RETURN 1

@SKILL_1504_SP_0_NAME
RESULTS = 달콤한 포화

;레벨은 1-5까지
@SKILL_1504_SP_0_LEVEL
RETURN 3

;대상 선택
@SKILL_1504_SP_0_SETTARGET(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
;부대 대상
RETURN 1

;발동 판정
@SKILL_1504_SP_0_CAN_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
RETURN 1

;발동 텍스트를 오바라이드 하고 싶을 때에.
;「아무개의 스킬 발동!   00했다!」의 「00했다!」의 부분을 실장하고 싶은 경우는,
;이것이 아니고 INVOKE로 쓰면 좋습니다.
;@SKILL_1504_SP_0_INVOKE_TEXT(발동자, 스킬, 장르)
;#DIM 발동자
;#DIM 스킬
;#DIMS 장르

;효과를 여기에 기술
@SKILL_1504_SP_0_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
; PRINTFORML "느긋히 먹을 틈도 없네!"
LOCAL:1 = RAND(2, 12)
FOR LOCAL, 0, LOCAL:1
	CALL DECREASE_SOLDIER(대상세력, 대상부대, RAND(ABL:발동자:무투*2 , ABL:발동자:무투*5), 1)
NEXT


@SKILL_1504_SP_0_EXPLANATION
RESULTS = 자신의 무투에 비례해서 연속 데미지를 준다.

@SKILL_1504_SP_0_CANT_TELL

; 아이가 배울수 없도록 한다
@SKILL_1504_SP_0_CANT_INHERIT


@SKILL_1504_SP_0_RATE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
RETURN 500

; #region == 총력전 ==
@총력전_스킬_1504_코스트(시전자)
#DIM 시전자 ; 코스트 당 대미지 스킬 카운터 위해서
; #DIM 가변
RETURN 4

; 중첩 2가 3보단 맞추긴 쉬운듯. 대미지 못맞추겠으면 2 중첩 으로 변경
; 변경시 3버프때 기본공격 DPS 안 떨어지게 주의
; 방디부는 공속에 버프, 최소한 디버프는 안 받게?
@총력전_스킬_1504_설명(시전자)
#DIM 시전자 ; 가변스킬 설명 위해서
; 1770%
RESULTS = 자신에게『퍼스트 테이스트』부여후 적에게 공격력 590％x3 대미지. 기본 공격 3회 마다 자신에게『퍼스트 테이스트』부여. 『퍼스트 테이스트』3턴 동안 공격 속도 감소(-9) 공격력 증가 (17％) 최대 3회까지 중첩 가능.
; RESULTS = 기본 공격 3회 마다 자신에게『퍼스트 테이스트』부여 (공속 감소 = -9)(공격력 증가=17％)(3턴)(최대 3회까지 중첩). 스킬 시전시 자신에게『퍼스트 테이스트』부여후 적에게 공격력 590％x3 대미지.


; 총력전 시작시 시전
@총력전_스킬_1504_초기(시전자)
#DIM 시전자
; 카운터(『퍼스트 테이스트』) 추가
CALL 총력전_카운터_추가(시전자, 시전자, 총력전_방식_공격, 총력전_방식_지속, 1, 1504)
; 테스트 텍스트 추가?


@총력전_스킬_1504_발동(시전자)
#DIM 시전자

#DIM DYNAMIC 목표
#DIM DYNAMIC 대미지
#DIM DYNAMIC iTmp
#DIMS DYNAMIC sTmp, 3

; 발동 텍스트
sTmp:0 = 「느긋히 먹을 틈도 없네!」
sTmp:1 = 「…아아, 그쪽?」
sTmp:2 = 「나쁘게 생각 말라고.」
CALL SKILL_1504_SP_0_NAME
CALL 총력전_스킬_공통_발동텍스트(시전자, 64, RESULTS, sTmp)
; 목표 처리
CALL 총력전_시퀀스_목표선택(목표, 총력전_선택_적군)
; 목표 없으면 스킬 해재
IF 목표 < 0
	CALL 총력전_스킬_공통_스킬취소(시전자)
	RETURN
ENDIF
; 버프
CALL _총력전_스킬_1504_버프추가(시전자)
; 대미지
대미지 = GETVAR_RAIDSTAT_SUM(시전자, "공격력") * 590
대미지 = DIVIDE_ROUND(대미지, 100)
FOR iTmp, 0, 3
	CALL 총력전_처리_피격(대미지, 목표, 시전자)
NEXT


; 공격력증가량 = 49
; 대미지 = (총력전_공격력:LOCAL * 1773 / 100) * (100 - 총력전_보스_방어력) / 100
; CALL 버프처리(1,,,,,공격력증가량,,,,,,,,1504,,LOCAL)
; CALL 공격판정처리(대미지,LOCAL)

; @총력전_스킬_1504_버프지속
; RETURN 2


; #region == 카운터 ==
; 기본공격 카운터
@총력전_스킬_1504_0_카운터_증가(캐릭터, 대상, 카운터번호, 증가양)
#DIM 캐릭터
#DIM 대상
#DIM 카운터번호
#DIM 증가양

#DIM DYNAMIC iTmp
#DIMS DYNAMIC sTmp

총력전_카운터_증가양:캐릭터:카운터번호 += 증가양
; 양 3 이상이면 버프 + 증가양 리셋
IF 총력전_카운터_증가양:캐릭터:카운터번호 >= 3
	CALL _총력전_스킬_1504_버프추가(캐릭터)
	총력전_카운터_증가양:캐릭터:카운터번호 = 0
ENDIF

; #region == 개인 함수? ==
; 에라엔 private function 이 없는걸로 알지만...
; 이미 적용 되있는 버프 확인 후 빈 추가번호로 맞춰서 추가
@_총력전_스킬_1504_버프추가(캐릭터)
#DIM 캐릭터

#DIM DYNAMIC 상효번호, 3 ; 이미 적용되있는 버프
#DIM CONST 버프최대수 = 3
#DIM DYNAMIC iTmp, 2
#DIMS DYNAMIC sTmp

FOR iTmp, 0, 버프최대수
	; iTmp 번 있는지 확인
	상효번호:iTmp = 총력전_상태효과_검색_스킬번호(캐릭터, 1504, iTmp)
	IF 상효번호:iTmp < 0
		; 텍스트 추가
		sTmp = 『퍼스트 테이스트』
		CALL 총력전_UI_텍스트_추가(sTmp, 칼라_노랑)
		iTmp:1 = 총력전_ID:캐릭터
		IF iTmp:1 >= 0 && NO:(iTmp:1) == 64
			sTmp = 좋아, 가자.
			CALL 총력전_UI_텍스트_추가(sTmp)
		ENDIF
		; UI 업데이트
		CALL 총력전_UI_Main(0)
		WAITANYKEY
		; 버프/디버프 적용
		CALL 총력전_상태효과_추가(캐릭터, 캐릭터, 상효_종류_공격력백분율, 17, 1, 총력전_방식_지속, 총력전_방식_턴, 3, 1504, iTmp)
		CALL 총력전_상태효과_추가(캐릭터, 캐릭터, 상효_종류_공격속도, -9, 0, 총력전_방식_지속, 총력전_방식_턴, 3, 1504, iTmp + 버프최대수)
		; 텍스트 추가
		sTmp = 3턴간 %조사처리(총력전_이름:캐릭터, "의")% 공격력 (17％) 증가, 공격 속도 (-9％) 감소!
		CALL 총력전_UI_텍스트_추가(sTmp, 칼라_노랑)
		; UI 업데이트
		CALL 총력전_UI_Main(0)
		WAITANYKEY
		BREAK
		; RETURN
	ENDIF
NEXT
; 