;-----------------------------------
;기본치 계산에 앞서 처리하는 것
;-----------------------------------
@SKILL_1121_SP_0_EXIST
RETURN 1

@SKILL_1121_SP_0_NAME
RESULTS = 시스템: 아비 에슈흐

;레벨은 1-5까지
@SKILL_1121_SP_0_LEVEL
RETURN 5

;대상 선택
@SKILL_1121_SP_0_SETTARGET(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
;부대 대상

RETURN 1

;발동 판정
@SKILL_1121_SP_0_CAN_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
 
RETURN 1

;발동 텍스트를 오바라이드 하고 싶을 때에.
;「아무개의 스킬 발동!   00했다!」의 「00했다!」의 부분을 실장하고 싶은 경우는,
;이것이 아니고 INVOKE로 쓰면 좋습니다.
;@SKILL_1121_SP_0_INVOKE_TEXT(발동자, 스킬, 장르)
;#DIM 발동자
;#DIM 스킬
;#DIMS 장르

;효과를 여기에 기술
@SKILL_1121_SP_0_INVOKE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
대상측 = !발동측
; PRINTFORML "장착 완료."
PRINTFORML %ANAME(발동자)%의 무투와 방위와 지략이 증가했다!
TIMES BATTLE_무투파워:발동측:발동번호, 2
TIMES BATTLE_방위파워:발동측:발동번호, 2
TIMES BATTLE_지략파워:발동측:발동번호, 2

@SKILL_1121_SP_0_EXPLANATION
RESULTS = 자신 부대의 무투와 방위와 지략이 증가한다.

@SKILL_1121_SP_0_CANT_TELL


@SKILL_1121_SP_0_RATE(발동자, 발동번호, 발동측, 발동세력, 발동부대, 대상세력, 대상부대)
#DIM 발동자
#DIM 발동번호
#DIM 스킬
#DIMS 장르
#DIM 발동측
#DIM 발동세력
#DIM 발동부대
#DIM 대상측
#DIM 대상세력
#DIM 대상부대
RETURN 700


; #region == 총력전 ==
@총력전_스킬_1121_코스트(시전자)
#DIM 시전자 ; 코스트 당 대미지 스킬 카운터 위해서
; #DIM 가변
IF 총력전_스킬_가변:시전자 == 0
	RETURN 2
ELSE ; 1, 2, 3
	RETURN 5
ENDIF

@총력전_스킬_1121_쿨다운(가변)
#DIM 가변
; + 가변 0 이면 쿨 0?
IF 가변 == 3 ; 막타후 긴 쿨다운
	RETURN 4
ELSE
	RETURN 1
ENDIF

@총력전_스킬_1121_설명(시전자)
#DIM 시전자 ; 가변스킬 설명 위해서
; 시전자 총력전_스킬_가변 확인후 RETURN
SELECTCASE 총력전_스킬_가변:시전자
	CASE 0
		RESULTS = 아비 에슈흐 모드로 전환, 자신의 공격력 41％ 명중률 18％ 회피율 12％ 상승
	CASE 3
		RESULTS = 적에게 방어력 60％ 감소후 공격력 1152％ 대미지 
	CASEELSE ; 1, 2
		RESULTS = 적에게 방어력 60％ 감소후 공격력 300％ 대미지 
ENDSELECT

; @총력전_스킬_1121_발동(시전자)
; #DIM 시전자


; 총력전_공격력:LOCAL += 41
; 총력전_명중률:LOCAL += 18
; 총력전_회피율:LOCAL += 12
; 가변스킬관리:LOCAL = 1

; TRYCALLFORM 총력전_스킬_{TALENT:(총력전_파티_캐릭터:LOCAL):웨폰 + 1000}_ 설명
; CALL 총력전_전투_텍스트_추가(@"%RESULTS%")

; 총력전에서만 쓰는거라 이미지 변경은 일단 여기서...
; 스킬 이름 변경
@총력전_스킬_1121_발동(시전자)
#DIM 시전자

#DIM DYNAMIC 목표
#DIM DYNAMIC 대미지
#DIM CONST 공격증가 = 41
#DIM CONST 명중증가 = 18
#DIM CONST 회피증가 = 12
#DIM DYNAMIC iTmp, 2
#DIMS DYNAMIC sTmp, 3

IF 총력전_스킬_가변:시전자 == 0 ; 모드 변화
	; 발동 텍스트
	sTmp:0 = 「아비 에슈흐, 이행.」
	sTmp:1 = 「파워드 슈트, 기동.」
	sTmp:2 = 「장착 완료.」
	; CALL SKILL_1121_SP_0_NAME
	RESULTS = 아비 에슈흐: 기동
	CALL 총력전_스킬_공통_발동텍스트(시전자, 45, RESULTS, sTmp)
	; 이미지 변화
	IF 시전자 >= 0 && 총력전_ID:시전자 == 45
		; 나중에 아비 에슈흐 부상 화상 추가 
		; + 체력확인 후 화상 설정
		IF RAND:2 == 0
			; 총력전시 얼굴 화상만 쓰니까
			총력전_이미지:시전자 = 얼굴_전투_B1_45
		ELSE
			총력전_이미지:시전자 = 얼굴_전투_B2_45
		ENDIF
	ENDIF
	; 버프, 긍부정 2? 지금은 버프 제거 스킬에 제거 될수있음
	CALL 총력전_상태효과_추가(시전자, 시전자, 상효_종류_공격력백분율, 공격증가, 1, 총력전_방식_지속, 총력전_방식_지속, 1, 1121, 0)
	CALL 총력전_상태효과_추가(시전자, 시전자, 상효_종류_명중률백분율, 명중증가, 1, 총력전_방식_지속, 총력전_방식_지속, 1, 1121, 1)
	CALL 총력전_상태효과_추가(시전자, 시전자, 상효_종류_회피율백분율, 회피증가, 1, 총력전_방식_지속, 총력전_방식_지속, 1, 1121, 2)
	; 텍스트 추가
	sTmp = %조사처리(총력전_이름:시전자, "의")% 공격력 ({공격증가}％), 명중률 ({명중증가}％), 회피율 ({회피증가}％) 증가
	CALL 총력전_UI_텍스트_추가(sTmp, 칼라_노랑)
	; UI 업데이트
	CALL 총력전_UI_Main(0)
	WAITANYKEY
	; 가변 번호
	총력전_스킬_가변:시전자 = 1
ELSE ; 가변 1, 2, 3
	; 발동 텍스트
	sTmp:0 = 「목표 확인. 조준.」
	sTmp:1 = 「발사!」
	sTmp:2 = 「파이어!」
	; CALL SKILL_1121_SP_0_NAME
	RESULTS = 아비 에슈흐: 섬멸 모드
	CALL 총력전_스킬_공통_발동텍스트(시전자, 45, RESULTS, sTmp)
	; 목표 처리
	CALL 총력전_시퀀스_목표선택(목표, 총력전_선택_적군)
	; 목표 없으면 스킬 해재
	IF 목표 < 0
		CALL 총력전_스킬_공통_스킬취소(시전자)
		RETURN
	ENDIF
	; 적 디버프, 방어 60% 무시 비슷하게 하는거라 (방어_초기 만 하기는함...) 텍스트 필요 없음(?)
	; 나중에 방어력 무시 디버프 더하면 변경
	CALL 총력전_상태효과_추가(목표, 시전자, 상효_종류_방어력백분율, -60, 0, 총력전_방식_피격, 총력전_방식_피격, 1, 1121, 3)
	; CALL 총력전_상태효과_스탯처리(목표, 총력전_방식_스킬)
	; 대미지
	IF 총력전_스킬_가변:시전자 == 3
		대미지 = GETVAR_RAIDSTAT_SUM(시전자, "공격력") * 1152
	ELSE
		대미지 = GETVAR_RAIDSTAT_SUM(시전자, "공격력") * 300
	ENDIF
	대미지 = DIVIDE_ROUND(대미지, 100)
	CALL 총력전_처리_피격(대미지, 목표, 시전자)
	; 회피 하면 상태효과 소비 안함...
	; 방어 무시 추가하면 필요 없어짐
	IF RESULT == 0
		iTmp = 총력전_상태효과_검색_스킬번호(목표, 1121, 3)
		SIF iTmp >= 0
			CALL 총력전_상태효과_제거_텍스트(목표, iTmp)
	ENDIF
	; 이상한 pause... 처리_피격 waitanykey 무시? 제거 텍스트에 2번 기다림
	; 가변 처리
	IF 총력전_스킬_가변:시전자 == 3
		; 버프 제거
		FOR iTmp, 0, 3
			iTmp:1 = -1
			[IF_DEBUG]
			sTmp = searching 1121.{iTmp}
			CALL 총력전_UI_텍스트_추가(sTmp, 칼라_파노랑)
			[ENDIF]
			iTmp:1 = 총력전_상태효과_검색_스킬번호(시전자, 1121, iTmp)
			SIF iTmp:1 >= 0
				CALL 총력전_상태효과_제거_텍스트(시전자, iTmp:1)
		NEXT
		; 가변 번호
		총력전_스킬_가변:시전자 = 0
		; 이미지 변화
		총력전_이미지:시전자 = %총력전_이미지_초기:시전자%
	ELSE
		; 가변 번호
		총력전_스킬_가변:시전자 ++
	ENDIF
ENDIF

